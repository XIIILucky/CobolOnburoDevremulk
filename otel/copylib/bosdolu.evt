* bosdolu.evt
* bosdolu.evt is generated from D:\asya\acugt.ytl\otel\bosdolu.Psf
* This is a generated file. DO NOT modify this file directly.

***   start event editor code   ***
*
 Form0-Aft-Create.
     perform adresleri-tasi
     
     open input genel
     move 1 to genel-anahtar
     read genel no lock
       invalid
         continue
       not invalid
         continue  
     end-read
     close genel

     perform rapor-basla
     .
*
 rapor-basla.
     open i-o genelfis
     move 1 to genelfis-anahtar
     read genelfis no lock
       invalid
         accept print-no from time
       not invalid
         add 1 to print-no
         rewrite genelfis-rec, end-rewrite
     end-read
     close genelfis

     initialize dokumer-rec
                dokumer-anah
                dokumer-ozellikler-rec
                dokumer-dosya
                
     set  dokumer-asya-set    to true
     set  dokumer-yazici-text to true
     move print-no            to dokumer-dosya-adi

     open output dokumer

*/WINDOW TITLE
     initialize dokumer-rec detay
     move "W" to det-dokumer-20(1:)
     move "Bos-Dolu Oda Raporu" to det-filler
     write dokumer-rec from detay, end-write

*/DOKUMER OZELLIKLER-REC
     initialize dokumer-rec detay
     move "O" to det-dokumer-20(1:)
     move "E" to dokumer-oto-sayfa-basi
     move 56  to dokumer-oto-sayfa-satir
     move "|" to dokumer-detay-kolon-ayirici
     move "LLLLLLLLLLLLLLLLLLLL" to dokumer-align-occ
     move dokumer-ozellikler-rec to det-filler
     write dokumer-rec from detay, end-write

*/ BASLIKLAR
     initialize dokumer-rec detay
     move "1" to det-dokumer-20(10:1)
     move "B" to det-dokumer-20(1:1)
     move Calis-Gun to det-filler(1:2)
     move "/"       to det-filler(3:1)
     move Calis-Ay  to det-filler(4:2)
     move "/"       to det-filler(6:1)
     move Calis-Yil to det-filler(7:4)
     move " Tarihli Bos-Dolu Oda Raporu" to det-filler(11:)
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move "B" to det-dokumer-20(1:1)
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move "B" to det-dokumer-20(1:1)
     move all "-" to oda-bos-dolu
     move     "|" to oda-bos-fil(1)
     move     "|" to oda-bos-dolu-fil
     move   "<->" to oda-bos-dolu-ayrac
     move     "|" to oda-dolu-fil(1)
     move     "|" to oda-bos-dolu-renk-fil
     initialize oda-bos-dolu-renk
     move oda-bos-dolu to det-filler
     move "-"  to det-dokumer-20(5:1)
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move "B" to det-dokumer-20(1:1)
     move all space to oda-bos-dolu
     move "|Dolu Odalar" to oda-dolu-sutunlar
     move "|"            to oda-bos-dolu-fil
     move "<->"          to oda-bos-dolu-ayrac
     move "|Bos  Odalar" to oda-bos-sutunlar
     move "|"            to oda-bos-dolu-renk-fil
     initialize oda-bos-dolu-ayrac
                oda-bos-dolu-renk
     move oda-bos-dolu to det-filler
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move "B" to det-dokumer-20(1:1)
     perform cizgi-cek
     move oda-bos-dolu to det-filler
     move "-"  to det-dokumer-20(5:1)
     write dokumer-rec from detay, end-write

     perform rapor-detay

     close dokumer
     call dokumer-prog
       on exception
         perform callerr-proc
       not on exception
         cancel dokumer-prog
     end-call

     delete file dokumer
     set exit-pushed to true
     .
*
 rapor-detay.
     open input odalar
                konuk
                rez
     initialize odalar-rec
                toplamlar
     perform pipe-at
     move 1 to islem-sutun
     move odalist-ilk-oda to odalar-no
     start odalar key >= odalar-anah
       invalid
         initialize mesaj-link
         move 04 to mesaj-no
         perform mesaj-ver
       not invalid
         initialize fs-odalar
         perform with test after
                 until fs-odalar = "10"
           perform oda-oku
         end-perform
         if islem-sutun not = 1
            perform Rack-Yaz
         end-if
         perform toplam-yaz
     end-start
     close odalar
           konuk
           rez
     .
*
 oda-oku.
     read odalar next no lock
       end
         move "10" to fs-odalar
       not end
* Filtre Bilgilerini kontrol et         
         if (odalist-hayali    = "E" or
             odalar-hayali not = "H")            and

            (odalist-ilk-oda   = spaces or
             odalist-ilk-oda   >= odalar-no)     and

            (odalist-son-oda   = spaces or
             odalist-son-oda   <= odalar-no)     and

            (odalist-ilk-konum = spaces or
             odalist-ilk-konum >= odalar-konumu) and

            (odalist-son-konum = spaces or
             odalist-son-konum <= odalar-konumu) and

            (odalist-ilk-kat   = spaces or
             odalist-ilk-kat   >= odalar-kat)    and

            (odalist-son-kat   = spaces or
             odalist-son-kat   <= odalar-kat)    and

            (odalist-ilk-tip   = spaces or
             odalist-ilk-tip   >= odalar-tipi)   and

            (odalist-son-tip   = spaces or
             odalist-son-tip   <= odalar-tipi)

            perform konuk-oku
            perform rez-oku
            add 1 to toplam-oda
            if odalar-durumu = house-ao
               add 1 to toplam-arizali
               evaluate true
                 when bos-yaz
                   move "#" to oda-bos-fil(islem-sutun)
                 when dolu-yaz
                   move "#" to oda-dolu-fil(islem-sutun)
               end-evaluate
            end-if
            evaluate true
              when bos-yaz
                move odalar-no to oda-bos-no(islem-sutun)
                if konuk-git-tar = calisma-tarihi
                   add 1 to toplam-cout
                   move "*" to oda-bos-fil(islem-sutun)
                end-if
              when dolu-yaz
                add 1 to toplam-satilan
                move odalar-no to oda-dolu-no(islem-sutun)
            end-evaluate
            add 1 to islem-sutun
            if islem-sutun > sutun-sayisi
               perform rack-yaz
            end-if
         end-if
     end-read
     .
*
 konuk-oku.
     initialize konuk-rec
     move "I"       to konuk-durumu
     move odalar-no to konuk-odano
     start konuk key = konuk-oda
       invalid
         set bos-yaz to true
       not invalid
         initialize fs-konuk
         perform with test after
                 until fs-konuk = "10"
           read konuk next no lock
             end
               move "10" to fs-konuk
               set bos-yaz to true
             not end
               if konuk-odano not = odalar-no
                  move "10" to fs-konuk
                  set bos-yaz to true
               else
                  if konuk-fol-kodu = "R"
                     if konuk-git-tar = calisma-tarihi
                        set bos-yaz to true
                     else
                        set dolu-yaz to true
                     end-if
                     move "10" to fs-konuk
                  end-if
               end-if
           end-read
         end-perform
     end-start
     .
*
 rez-oku.
     initialize rez-rec
     move "I" to rez-durumu
     move calisma-tarihi to rez-gir-tar
     start rez key = rez-gir
       invalid
         continue
       not invalid
         initialize fs-rez
         perform with test after
                 until fs-rez = "10"
           read rez next no lock
             end
               move "10" to fs-rez
             not end
               if rez-gir-tar not = calisma-tarihi
                  move "10" to fs-rez
               else
                  if rez-odano = odalar-no
                     move "10" to fs-rez
                     if rez-c-in not = "E"
                        move "&" to oda-dolu-fil(islem-sutun)
                        move "&" to oda-bos-fil(islem-sutun)
                        add 1 to toplam-blokaj
                     end-if
                  end-if
               end-if
           end-read
         end-perform
     end-start
     .
*
 rack-yaz.
     initialize dokumer-rec detay
     move oda-bos-dolu to det-filler
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     perform cizgi-cek
     move oda-bos-dolu to det-filler
     write dokumer-rec from detay, end-write
     
     perform pipe-at
     move 1 to islem-sutun
     .
*
 toplam-yaz.
     initialize dokumer-rec detay
     write dokumer-rec from detay, end-write
     
     initialize dokumer-rec detay
     move toplam-oda     to toplam-goster
     move toplam-satilan to satilan-goster
     move toplam-blokaj  to blokaj-goster
     move toplam-cout    to cout-goster
     move toplam-arizali to arizali-goster
     move toplam-satiri  to det-filler
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move "[*]->Check Out Oda   [#]->Arizali Oda   [&]->Blokaj Oda" to det-filler
     write dokumer-rec from detay, end-write
     .
*
 pipe-at.
     initialize oda-bos-dolu
     perform varying islem-sutun
             from    1
             by      1
             until   islem-sutun > sutun-sayisi
       move "|" to oda-dolu-fil(islem-sutun)
       move "|" to oda-bos-fil(islem-sutun)
     end-perform
     move "|"   to oda-bos-dolu-fil
     move "<->" to oda-bos-dolu-ayrac
     move "|"   to oda-bos-dolu-renk-fil
     initialize oda-bos-dolu-renk
     .
*
 cizgi-cek.
     move all "-" to oda-bos-dolu
     perform varying islem-sutun
             from    1
             by      1
             until   islem-sutun > sutun-sayisi
       move "|" to oda-dolu-fil(islem-sutun)
       move "|" to oda-bos-fil(islem-sutun)
     end-perform
     move "|"   to oda-bos-dolu-fil
     move "<->" to oda-bos-dolu-ayrac
     move "|"   to oda-bos-dolu-renk-fil
     initialize oda-bos-dolu-renk
     .
*
*
 Form0-Aft-Initdata.
*/-----------------------------\*  
*/---------DIL AYARLA----------\*    
*/-----------------------------\*    
     perform dil-ayarla-proc.
*/-----------------------------\*         
     .
 
