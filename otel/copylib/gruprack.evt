* gruprack.evt
* gruprack.evt is generated from D:\asya\acugt.ytl\otel\gruprack.Psf
* This is a generated file. DO NOT modify this file directly.


 Form1-Event-Proc.
     .

 Form1-Exception-Proc.
     PERFORM Form1-Ex-Other
     .

 Form1-Gd-1-Event-Proc.
     PERFORM Form1-Gd-1-Ev-Other
     .
***   start event editor code   ***
*
 Form1-Bef-Create.
     perform adresleri-tasi
     move tarih-tasi to filtre-grup-bugun-tar
     move yil-tasi  to filtre-giris-yil 
     compute filtre-giris-yils = yil-tasi + 1
**     move 1         to filtre-giris-gun filtre-giris-ay
     move 31        to filtre-giris-guns
     move 12        to filtre-giris-ays
     move tarih-tasi to filtre-giris-tar
     .
*
 Form1-Aft-Initdata.
     perform baslik-yukle
     perform grid-yukle.
*
 baslik-yukle.
     modify form1-gd-1,reset-grid = 1
                       mass-update = 1
     initialize form1-gd-1-record
     move "Kodu"         to gd-1-col-1
     move "Grup Adi"     to gd-1-col-2
     move "C/In Tarihi"  to gd-1-col-3
     move "C/Out Tarihi" to gd-1-col-4
     move "Durum"        to gd-1-col-5
     move "Grup Durumu"  to gd-1-col-6
     move "Silme / Iptal Nedeni"        to gd-1-col-7
     move "Acenta"       to gd-1-col-8
     move "Firma"        to gd-1-col-9
     modify form1-gd-1,record-to-add(form1-gd-1-record)
     .
*
 grid-yukle.
     open i-o genelfis
     initialize genelfis-anahtar
     move 1      to genelfis-anahtar
     read genelfis no lock invalid
          continue 
     not invalid 
         add 1 to ekran-fis-1
         rewrite genelfis-rec end-rewrite 
         move ekran-fis-1 to tgruplar-no
     end-read 
     close genelfis

     move k-kodu-tasi    to tgruplar-k-kodu
     open output tgruplar close tgruplar open i-o tgruplar       

     open i-o gruplar blok acenta firma

     perform takas-yaz

     initialize tgruplar-rec toplam-kayit iptal-kayit sil-kayit
     move 1            to i
     evaluate siralama(1:1)
     when "K"
             if filtre-grup-no not = spaces
                move function numval(filtre-grup-no)   to tgruplar-anahtar 
             end-if 
             start tgruplar key not < tgruplar-anahtar invalid
                   close gruplar blok tgruplar acenta firma
                   exit paragraph 
             end-start 
     when "G"
             start tgruplar key not < tgruplar-gel-tar invalid
                   close gruplar blok tgruplar acenta
                   exit paragraph  
             end-start 
     when "C"
             start tgruplar key not < tgruplar-git-tar invalid
                   close gruplar blok tgruplar acenta
                   exit paragraph  
             end-start 
     when "A"
             start tgruplar key not < tgruplar-adi invalid
                   close gruplar blok tgruplar acenta
                   exit paragraph  
             end-start               
     end-evaluate      

     perform with test after until fs-tgruplar = "10"
     read tgruplar next no lock end move "10"  to fs-tgruplar
     not at end
            if tgruplar-grup-statu not = filtre-grup-statu(1:1) and
               filtre-grup-statu(1:1) not = 0
                   exit perform cycle 
            end-if 
            if filtre-grup-no not = spaces
               if tgruplar-kodu <> function numval(filtre-grup-no)
                   exit perform cycle 
               end-if 
            end-if 
            if filtre-grup-adi <> spaces
                 move tgruplar-adi       to kelime
                 move filtre-grup-adi   to aranan-kelime
                 move 0 to aranan-i aranan-kelime-boy
                 inspect aranan-kelime tallying aranan-kelime-boy
                         for trailing spaces 
                 compute aranan-kelime-boy = function length(aranan-kelime) -
                                             aranan-kelime-boy
        
                 inspect kelime tallying aranan-i 
                         for all aranan-kelime(1:aranan-kelime-boy)
                 if aranan-i = 0                 
                    exit perform cycle
                 end-if
            end-if
            if filtre-durumu(1:1) = "G"       
               if tgruplar-durumu = "S" or tgruplar-durumu = "I"
                  exit perform cycle 
               end-if 
            end-if
            if filtre-durumu(1:1) not = "G"
               if tgruplar-durumu not = filtre-durumu(1:1) and
                  filtre-durumu(1:1) not = "H"
                     exit perform cycle 
               end-if
            end-if
            if grup-bugun = 0         
               if tgruplar-gel-tar < filtre-giris-tar or 
                  tgruplar-gel-tar > filtre-giris-tars
                     exit perform cycle 
               end-if               
            end-if 

            if grup-bugun = 0
               if tgruplar-git-tar not = filtre-cikis-tar and
                  filtre-cikis-tar not = spaces and 
                  filtre-cikis-tar not = zeroes 
                     exit perform cycle 
               end-if               
            end-if 
            if grup-bugun = 1
               if filtre-grup-bugun-tar >= tgruplar-gel-tar and
                  filtre-grup-bugun-tar <= tgruplar-git-tar 
                     continue 
               else 
                  exit perform cycle 
               end-if 
            end-if
            if tgruplar-acenta not = acn-kod and
               acn-kod not = spaces 
                  exit perform cycle 
            end-if 
            if tgruplar-firma not = firma-kod and
               firma-kod not = spaces 
                  exit perform cycle 
            end-if 

            add 1                   to i toplam-kayit
            initialize form1-gd-1-record
            move tgruplar-kodu      to gd-1-col-1
            move tgruplar-adi       to gd-1-col-2

            move tgruplar-gel-gun   to egun
            move tgruplar-gel-ay    to eay
            move tgruplar-gel-yil   to eyil
            move etar               to gd-1-col-3
           
            move tgruplar-git-gun   to egun
            move tgruplar-git-ay    to eay
            move tgruplar-git-yil   to eyil
            move etar               to gd-1-col-4

            evaluate tgruplar-durumu
            when "S"
                 move "Silindi"     to gd-1-col-5
                 add 1              to sil-kayit
            when "I"
                 move "Iptal"       to gd-1-col-5
                 add 1              to iptal-kayit
            end-evaluate
            evaluate tgruplar-grup-statu
            when 1
                move "Pending"      to gd-1-col-6
            when 2
                move "Optional"     to gd-1-col-6
            when 3
                move "Tentative"    to gd-1-col-6
            when 4
                move "Definitive"   to gd-1-col-6
            end-evaluate

            move tgruplar-silme-iptal-nedeni to gd-1-col-7

            initialize acenta-rec
            move tgruplar-acenta             to acenta-kodu
            read acenta no lock invalid 
                 move "-Tanimsiz.."          to acenta-adi
            end-read 
                 move acenta-adi             to gd-1-col-8
            if tgruplar-firma not = spaces 
               initialize firma-rec
               move tgruplar-firma              to firma-kodu
               read firma no lock invalid 
                    move "-Tanimsiz.."          to firma-adi
               end-read 
                    move firma-adi              to gd-1-col-9
            end-if 
            modify form1-gd-1,
                 record-to-add(form1-gd-1-record)
            evaluate tgruplar-durumu
            when "S"
                    modify form1-gd-1(i),
                         row-color = 176
            when "I"
                    modify form1-gd-1(i),
                         row-color = 289
            end-evaluate

     end-read
     end-perform     
     close gruplar blok tgruplar acenta firma
     modify form1-gd-1,mass-update = 0
     compute islem-kayit = toplam-kayit - (sil-kayit + iptal-kayit)
     display acc-toplam-kayit acc-sil-kayit acc-iptal-kayit acc-islem-kayit
     delete file tgruplar 
     .
*
 Form1-Gd-1-Ev-Other.
     evaluate event-type
     when msg-begin-entry              
          move event-action-fail to event-action
          inquire form1-gd-1(event-data-2,1),cell-data in link-grup-kodu
          open input gruplar
          initialize gruplar-rec
          move link-grup-kodu to gruplar-kodu
          read gruplar no lock invalid 
               display message box "Secim Yapilamadi..." new-line
                                   "Lutfen Tekrar Deneyiniz..."
                               title "Uyari"
                               icon 1
                        close gruplar
                 exit paragraph 
          end-read 
          close gruplar
               if gruplar-durumu = "S"
                  display message box "Silinen Grup Uzerinde Degisiklik Yapilamaz.."
                                  title "Uyari"
                                  icon 1
               else
                  call "/asya/ytl/obj/otel/gruplar.asy" 
                                using  link-grup-kodu
                                on exception 
                                perform callerr-proc
                                exit paragraph
                                not on exception 
                           cancel "/asya/ytl/obj/otel/gruplar.asy"
                 end-call
                   call "w$keybuf" using 3 end-call

               end-if 
     end-evaluate     
     .
*
 Form1-Ex-Other.
     evaluate key-status
     when 1
        evaluate control-id
        when 44
        when 2001
             initialize acenta-cagir
             call "/asya/ytl/obj/otel/acenara.asy" 
                  using acenta-cagir  
                  on exception 
                     perform callerr-proc
                  not on exception
                     if acenta-cagir <> spaces
                        move acenta-cagir to acn-kod
                        display acc-07
                     end-if
             end-call
             move 4 to accept-control
             move 44 to control-id
              perform acen-oku
        when 46
        when 47
             initialize firma-cagir
             call "/asya/ytl/obj/otel/firmara.asy" 
                  using firma-cagir  
                  on exception 
                     perform callerr-proc
                  not on exception
                     if firma-cagir <> spaces
                        move firma-cagir to firma-kod
                        display acc-firma
                     end-if
             end-call
             move 4 to accept-control
             move 46 to control-id
             perform firma-oku
        end-evaluate 
     when 2          
          initialize link-grup-kodu
          call "/asya/ytl/obj/otel/gruplar.asy" 
                        using  link-grup-kodu
                        on exception 
                        perform callerr-proc
                        exit paragraph
                        not on exception 
               cancel "/asya/ytl/obj/otel/gruplar.asy"
         end-call        
           call "w$keybuf" using 3 end-call

         perform baslik-yukle
         perform grid-yukle
     when 5
         perform acen-oku
         perform firma-oku
         perform baslik-yukle
         perform grid-yukle 
     when 4
         perform raporla
     end-evaluate
     
     .
*
 takas-yaz.     
     initialize gruplar-rec
     start gruplar key not < gruplar-anahtar invalid
           continue 
     not invalid 
     perform with test after until fs-gruplar = "10"
     read gruplar next no lock end move "10" to fs-gruplar
     not at end 
            initialize tgruplar-rec
            move gruplar-rec   to tgruplar-rec
            write tgruplar-rec invalid 
                display message box "Hata"
            end-write 
     end-read 
     end-perform
     end-start
     .
*
 raporla.

              open i-o genelfis
              move 1 to genelfis-anahtar
              read genelfis no lock invalid
                   accept print-no from time
              not invalid
                   add 1 to print-no
                   rewrite genelfis-rec end-rewrite
              end-read
              close genelfis
**************************
     initialize dokumer-rec dokumer-anah dokumer-ozellikler-rec
                dokumer-dosya

     set  dokumer-asya-set to true
     move print-no         to dokumer-dosya-adi
     open output dokumer
*/WINDOW TITLE
     initialize dokumer-rec detay
     move "W"                  to det-dokumer-20(1:)
     move "Grup Listesi" to det-filler
     write dokumer-rec from detay
*/ BASLIKLAR
     initialize dokumer-rec detay
     move "1"          to det-dokumer-20(10:1)
     move "B"                  to det-dokumer-20(1:1)
     move "Grup Listesi" to det-filler
      
     write dokumer-rec from detay

     initialize dokumer-rec detay
     move "B"                  to det-dokumer-20(1:1)
     write dokumer-rec from detay

*/ DOKUMER OZELLIKLER-REC
     initialize dokumer-rec detay
     move "O" to det-dokumer-20(1:)
     move "E" to dokumer-oto-sayfa-basi
     move 56  to dokumer-oto-sayfa-satir
     move "|" to dokumer-detay-kolon-ayirici
     move "LLLLLLLLLLLLLLLLLL" to dokumer-align-occ
     move dokumer-ozellikler-rec     to det-filler
     write dokumer-rec from detay
*/ BASLIKLAR  

     initialize dokumer-rec detay 
     move "1"          to det-dokumer-20(10:1)
     move "D1"                          to det-dokumer-20(1:2)     
     move "Kodu"                        to det-1
     move "Grup Adi"                    to det-2
     move "C/in Tarihi"                 to det-3
     move "C/Out Tarihi"                to det-4
     move "Durum    "                   to det-5
     move "G.Durum"                     to det-6
     move "Silme/Iptal Sebebi"          to det-7
     move "Acenta"                      to det-8
     move "Firma"                       to det-9
     move "|" to fil-1 fil-2 fil-3 fil-4 fil-5 fil-6
                 fil-7 fil-8 fil-9
     write dokumer-rec from detay

     initialize dokumer-rec detay
     move "D2"           to det-dokumer-20(1:2)
     move "-"            to det-dokumer-20(5:1)
     move all "-" to det-1 det-2 det-3 det-4 det-5 det-6
                 det-7  det-8    det-9
     write dokumer-rec from detay
**************************
     inquire form1-gd-1,last-row in son-satir
     initialize liste-toplam
     perform varying i from 2 by 1 until i > son-satir
                           add 1 to liste-toplam
                           initialize dokumer-rec detay
                           inquire form1-gd-1(i),record-data form1-gd-1-record
                           move gd-1-col-1  to det-1
                           move gd-1-col-2  to det-2 
                           move gd-1-col-3  to det-3
                           move gd-1-col-4  to det-4
                           move gd-1-col-5  to det-5 
                           move gd-1-col-6  to det-6
                           move gd-1-col-7  to det-7
                           move gd-1-col-8  to det-8
                           move gd-1-col-9  to det-9

                           move "|" to fil-1  fil-2   fil-3  fil-4  fil-5  fil-6
                                       fil-7  fil-8   fil-9
                           write dokumer-rec from detay
     end-perform 

     initialize dokumer-rec detay
     move "Toplam->"   to det-1
     move liste-toplam to z-top
     move z-top        to det-2
     move "A"          to det-dokumer-20(3:1)
     move 481          to det-renk1
     move "1"          to det-dokumer-20(10:1)
     write dokumer-rec from detay

     close dokumer
     call dokumer-prog on exception
          perform callerr-proc
     not on exception
          cancel dokumer-prog
     end-call              
     delete file dokumer.
*
 acen-oku.
     if acn-kod not = spaces
         open input acenta
         initialize acenta-rec 
         move acn-kod       to acenta-kodu
         read acenta no lock invalid 
              move "-Tanimsiz Acenta.." to acenta-adi
         end-read 
              move acenta-adi           to rap-acen-adi
         close acenta 
     else 
        move "Tum Acentalar.."  to rap-acen-adi
     end-if 
     display lb-acenadi
     .
*
 firma-oku.
     if firma-kod not = spaces
         open input firma
         initialize firma-rec 
         move firma-kod       to firma-kodu
         read firma no lock invalid 
              move "-Tanimsiz Firma.." to firma-adi
         end-read 
              move firma-adi           to rap-firma-adi
         close firma         
     else 
       move "Tum Firmalar.."  to rap-firma-adi
     end-if 
     display lb-firma
     .

 
