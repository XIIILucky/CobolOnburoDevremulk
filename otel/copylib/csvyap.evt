* csvyap.evt
* csvyap.evt is generated from D:\asya\acugt.ytl\otel\csvyap.Psf
* This is a generated file. DO NOT modify this file directly.


 Form1-Event-Proc.
     .
***   start event editor code   ***
*
 Form1-Bef-Create.
*************WARNINNGGGGGG WARMINGGGGGGGG***************
********
******* pic x(4000) olan degiskenleri dokumer-rec büyütüldüðünde sizeof dokumer-rec * 2 + 100 
********
********

     initialize ole-excel-degiskenler durum.
     move 0 to top-satir 
*     move "voyag010"     to isyeri-adres-tasi.
*     move "c:\asya\ytl\liste\apo"  to dokumer-dosya.
     perform adresleri-tasi.

*
 Form1-Aft-Initdata.
     open input genel
     move 1 to genel-anahtar
     read genel no lock invalid
          continue
     end-read
     close genel.

     open i-o genelfis
     move 1 to genelfis-anahtar
     read genelfis no lock invalid
          accept ekran-fis-1 from time
     end-read
     add 1 to ekran-fis-1
     rewrite genelfis-rec invalid
         write genelfis-rec end-write
     end-rewrite
     close genelfis.
     move ekran-fis-1     to liste-no

     modify acc-1,value = dokumer-dosya
     modify acc-2,
     modify acc-3,value = "Kayit sayisi aliniyor ..."
     modify acc-4,value = 01
       
     perform prosif-oku.
     perform get-line-count
     perform ole-excel-proc.

     perform upload-file.
     if hata 
        perform destroy-window-proc
        goback.

     if csvyap-link-islem-csv-yap
        move liste-adres     to csvyap-link-nereye-cek
        set exit-pushed      to true
        exit paragraph
     end-if

     perform excel-ole-create.


     if csvyap-link-islem-xls-yap
        move hedef    to csvyap-link-nereye-cek
     else     
        perform open-with-excel
     end-if.
     set exit-pushed to true.
*
 prosif-oku.
     open i-o prosif.
     initialize prosif-rec
     move k-kodu-tasi                     to prosif-kim
     move csvyap-link-called-program      to prosif-cob
     move "D"                             to prosif-tipi
     read prosif no lock invalid
          move all "1" to prosif-detaylar prosif-genel-ayarlar
          write prosif-rec invalid
                rewrite prosif-rec end-rewrite
          end-write
     end-read.
     close prosif.

*
 ole-excel-proc.
*
     initialize frame-degiskenler
     move top-satir            to frame-kayit-sayi
     move 100                  to frame-adet

     if frame-kayit-sayi < frame-adet
        compute frame-ciz = frame-adet / frame-kayit-sayi
        move 1 to frame-kayitta
     else
        compute frame-kayitta = frame-kayit-sayi / frame-adet
        move 1 to frame-ciz
     end-if.

     move 1 to frame-yuzde-pos.


     modify acc-1,value = dokumer-dosya
     modify acc-2,
     modify acc-3,value = "Rapor Dosyasi Olusturuluyor"
     modify acc-4,value = 01


     open input dokumer.
     accept liste-tarih     from century-date
     accept liste-zaman     from time
     open output liste

     initialize grid0-degiskenler fs-dokumer ole-excel-sutun
     ole-excel-satir sayac.
     

     move 1 to pnt
     move 1 to ole-excel-sutun ole-excel-satir ole-excel-b-sayi
     if not csvyap-link-islem-csv-yap
        perform sirket-bilgileri
        perform ole-excel-record-send
        perform write-liste-rec

        perform zaman-tarih-bilgileri
        perform ole-excel-record-send
        perform write-liste-rec
     end-if
     initialize ole-excel-bilgi

     perform with test after until fs-dokumer = "10"
     initialize dokumer-rec
     read dokumer next no lock end move "10" to fs-dokumer
     not at end
          perform yuzde-goster

          evaluate dokumer-20(1:1)
             when "W"

                   if csvyap-link-islem-csv-yap
                      exit perform cycle
                   end-if

                   move 1 to ole-excel-sutun 
                   add  1 to ole-excel-satir ole-excel-b-sayi
                   move dokumer-400    to ole-excel-bilgi
                   perform ole-excel-record-send
                   perform write-liste-rec
                   
             when "B"
                   |/////EXCEL 1//////////////////////////////////////////
                   if ref-baslik-yaz = 1 and ONKPARA-REFERANS-VAR = 1
                           perform fiyat-oda-ref-after
                           add 1                    to ole-excel-satir ole-excel-b-sayi 
                           move ref-baslik-detay    to ole-excel-bilgi
                           move 1 to pnt
                           perform ole-excel-record-send
                           perform write-liste-rec
                   end-if
                   |////////////////////////////////////////////////////////////////////
             
                   if csvyap-link-islem-csv-yap
                      exit perform cycle
                   end-if
                   add 1 to ole-excel-satir ole-excel-b-sayi
                   move dokumer-400         to ole-excel-bilgi
                   move 1 to pnt
                   perform ole-excel-record-send
                   perform write-liste-rec

             when "O"
                   move dokumer-400  to dokumer-ozellikler-rec

             when "D"
                   evaluate dokumer-20(2:1)
                       when 1
                            perform ole-excel-detay-al
                            perform varying i from 1 by 1 until i > grid0-detay-sayi
                                    if i <= 50
                                       move dokumer-align(i)   to grid0-align(i)
                                    end-if
                            end-perform
                            initialize i ii grid0-i grid0-ii grid0-kisit-detaylar
                                   grid0-kisit-detay-sayi
                            perform varying i from 1 by 1 until i > grid0-detay-sayi
                                   if prosif-detaylar(i:1) = "1" or 
                                      i > 50
                                      add 1           to grid0-kisit-detay-sayi
                                      move i          to grid0-kisit-index(grid0-kisit-detay-sayi)
                                      add grid0-au(i) to grid0-virtual-width
                                      add 1           to grid0-virtual-width
                                   end-if
                            end-perform
                            move 1 to grid0-iii iii
                            perform varying i from 1 by 1 until i = grid0-kisit-detay-sayi
                                    move grid0-kisit-index(i) to grid0-ii
                                    add  grid0-au(grid0-ii)   to grid0-iii
                                    add  1 to iii
                            end-perform
                            inspect dokumer-400 replacing all 
                                    dokumer-detay-kolon-ayirici
                                    by space
                   end-evaluate
                   if dokumer-20(5:1) = "-"
                      exit perform cycle
                   end-if
                   add 1 to ole-excel-satir ole-excel-d-sayi
                   move 1 to pnt
                   perform ole-excel-kayit-yerlestir

             when other
                   if dokumer-20(5:1) = "-"
                      exit perform cycle
                   end-if
                   add 1 to ole-excel-satir
                   move 1 to pnt
                   perform ole-excel-kayit-yerlestir

          end-evaluate
     end-read
     end-perform.
     close dokumer liste.
     
*
 ole-excel-kayit-yerlestir.
     initialize my-record-area i ii iii.
     move 1 to iii ole-excel-sutun pnt
     perform varying i from 1 by 1 until i > grid0-kisit-detay-sayi
             move grid0-kisit-index(i)      to ii
             move dokumer-rec(grid0-bp(ii):grid0-au(ii)) to 
                  ole-excel-bilgi
             add  grid0-au(ii)        to iii
             add  1                   to iii
             if ole-excel-bilgi <> spaces,  
                if ole-excel-bilgi(3:1)= "/" and 
                   ole-excel-bilgi(6:1)= "/" then
                   inspect ole-excel-bilgi replacing all "/" by "-"
*                   move ole-excel-bilgi(4:2) to tampon
*                   move ole-excel-bilgi(1:2) to ole-excel-bilgi(4:2)
*                   move tampon to ole-excel-bilgi(1:2)
*                   move "."    to ole-excel-bilgi(3:1)
*                                  ole-excel-bilgi(6:1)
                 end-if
             end-if
             perform ole-excel-record-send
             add  1                   to ole-excel-sutun
     end-perform.
     perform write-liste-rec.

*
 ole-excel-detay-al.
     initialize i ii grid0-i.
     inspect dokumer-rec tallying grid0-detay-sayi
             for all dokumer-detay-kolon-ayirici.
     perform varying i from function length(dokumer-rec) by -1 until i = 21
             if dokumer-rec(i:1) = dokumer-detay-kolon-ayirici
                move i to grid0-son-yer
                exit perform
             end-if
     end-perform.

     move 0  to grid0-ii
     move 1  to grid0-i     
     move 22 to grid0-bp(grid0-i)
     perform varying i from 22 by 1 until i > grid0-son-yer
             if dokumer-rec(i:1) = dokumer-detay-kolon-ayirici
                move ii to grid0-au(grid0-i)
                move 0  to ii
                add  1  to grid0-i
                move 0  to grid0-ii
             else
                add 1   to ii grid0-ii
                if grid0-ii = 1
                   move i     to grid0-bp(grid0-i)
                end-if
             end-if
     end-perform.
     perform varying i from 1 by 1 until i > grid0-detay-sayi
             move dokumer-rec(grid0-bp(i):grid0-au(i)) to 
                  grid0-detay-adi(i)
     end-perform.

 ole-excel-record-send.
    inspect ole-excel-bilgi,
            replacing all high-values by space
                      all low-values  by space.

    inspect ole-excel-bilgi replacing all '"' by space
    inspect ole-excel-bilgi replacing trailing spaces by low-values
    if ole-excel-sutun = 1
       move all low-values to liste-rec
       move 1 to pnt
       if ole-excel-satir = 1
          string x"FF" x"FE" delimited by size
                 into liste-rec
          move 3 to pnt
       end-if
    end-if

    move all low-values to my-record-area
*/ gerek kalmadi ....
*    if genel-nokta-virgul = "."
*       inspect ole-excel-bilgi
*               converting "." to x"01"
*       inspect ole-excel-bilgi
*               converting "," to "."
*       inspect ole-excel-bilgi
*               converting x"01" to ","
*    end-if


*    if genel-nokta-virgul = ","
      if k-ayrac-degistir = 1 
       inspect ole-excel-bilgi
               converting "." to x"01"
       inspect ole-excel-bilgi
               converting "," to x"03"
       
       inspect ole-excel-bilgi
               converting x"01" to ","
      inspect ole-excel-bilgi
               converting x"03" to "."
    end-if

    perform unicode-cevir

    move my-record-area(1:createfile-pnt)  to liste-rec(pnt:)
    add createfile-pnt     to pnt
    subtract 1 from pnt
    move x"0900"           to liste-rec(pnt:).
    add 2                  to pnt.

*
 unicode-cevir.
    move 1 to createfile-pnt createfile-i
    perform with test after 
            until ole-excel-bilgi(createfile-i:1) = low-values
              evaluate ole-excel-bilgi(createfile-i:1)
*/Ð
              when x"9E"
                   move x"5E01" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt
*/Ð
              when x"A6"
                   move x"1E01" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt
*/ º
              when x"98"
                   move x"3001" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt
*/=
              when x"99"
                   move x"D600" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt
*/-
              when x"9A"
                   move x"DC00" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt
*/+
              when x"80"
                   move x"C700" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt
*/á
              when x"A7"
                   move x"1F01" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt
*/-
              when x"8D"
                   move x"3101" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt
*/-
              when x"81"
                   move x"7101" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt
*/_
              when x"87"
                   move x"E701" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt
*/º
              when x"9F"
                   move x"5f01" to my-record-area(createfile-pnt:2)
                   add 2 to createfile-pnt

              when other
                   move ole-excel-bilgi(createfile-i:1) to my-record-area(createfile-pnt:1)
                   add 1 to createfile-pnt
                   move x"00" to my-record-area(createfile-pnt:1)
                   add 1 to createfile-pnt
              end-evaluate
              add 1 to createfile-i
      end-perform.
*
 write-liste-rec.
       move x"0D000A00" to liste-rec(pnt:)
       add 4 to pnt.
       move spaces     to liste-rec(pnt:)
       write liste-rec with no control.
       add 1 to kayit-sayi.

*
 upload-file.

       modify acc-1,value = dokumer-dosya
       modify acc-2,
       modify acc-3,value = "Dosya Yerele Aliniyor ..."
       modify acc-4,value = 01

       move all low-values to hedef kaynak.
       string hedef,
              "@[DISPLAY]:c:\acucorp\acucbl701\acugt\pdf_temp\" delimited by low-values 
              into hedef.
       if is-remote 
          move liste-adres       to kaynak
          inspect kaynak replacing  trailing spaces by low-values
          string hedef,
                 liste-file delimited by low-values
                 into hedef
          call "c$copy" using kaynak, hedef giving copy-status
          if copy-status <> 0
             display message box
                     "Rapor Dosyasi Yerele Kopyalanamadi !!!",x"0a0d"
                     "From : ",kaynak,x"0a0d"
                     "To :" ,hedef
                     icon mb_error_icon
                     title "Hata"
             set hata to true
          end-if
          move hedef(12:)     to hedef
       else
          move liste-adres    to hedef
          inspect hedef replacing all "/" by "\"
       end-if.

*
 opensave-proc.
       initialize opensave-data
       move "Excel dosyasinin kaydedilecegi klasoru seciniz - ASYA-SOFT" to opnsav-title
       MOVE "Excel dosyalari (*.csv)|*.csv" to opnsav-filters 

       move "csv"                to opnsav-default-ext
       call "c$opensavebox" using opensave-save-box,
                                  opensave-data
                            giving return-code

       if return-code < 1
          exit paragraph.

*
 open-with-excel.
       if is-remote 
          set shell32-dll-uzak to true
          set shell-execute-uzak to true
       else
          set shell32-dll-yerel to true
          set shell-execute-yerel to true
       end-if
       call shell32-dll on exception 
            display message box
                   "shell32.dll dosyasi bulunamiyor ....",x"0a0d",
                   "Excel dosyaniz otomatik olarak acilmayacaktir",x"0a0d",
                   "Kaydettiginiz dosyaya cift tiklayarak acabilirsiniz ... !"
                   icon mb_error_icon 
                   title "Hata"
            exit paragraph.

       inspect hedef replacing trailing spaces by low-values

       call shell-execute using by value 0,
                                by reference ac-komut
                                by reference hedef,
                                null
                                null
                                by value SW_SHOW
        on exception
            display message box
                   "ShellExecute Proseduru bulunamiyor ....",x"0a0d",
                   "Excel dosyaniz otomatik olarak acilmayacaktir",x"0a0d",
                   "Kaydettiginiz dosyaya cift tiklayarak acabilirsiniz ... !"
                   icon mb_error_icon 
                   title "Hata"
            cancel shell32-dll
            exit paragraph
       end-call.
       cancel shell-execute.
       cancel shell32-dll.


*
 get-line-count.
      if os-is-unix
         perform get-line-count-unix
      else
         perform get-line-count-win
      end-if.

*
 get-line-count-unix.
      move all low-values to wc-komut
      string wc-komut
             "-P wc -l < " delimited by low-values
             dokumer-dosya delimited by low-values
             into wc-komut

      open input wc
      initialize wc-rec
      read wc next no lock end-read
      close wc.
      move function numval-c(wc-rec)    to top-satir.

*
 get-line-count-win.
      move 0 to top-satir
      open input dokumer
      perform with test after until fs-dokumer = "10"
      initialize dokumer-rec
      read dokumer next no lock end move "10" to fs-dokumer
      not at end
           add 1 to top-satir
      end-read
      end-perform
      close dokumer.
*
 excel-ole-create.
      modify acc-3,value = "Dosya Aciliyor ..."
 
      Create @Application of @Excel 
             handle in xls-app.

      inquire xls-app, 
              version in excel-ver

      modify xls-app,
             @Visible = 0
             @Workbooks::@Add() giving xls-workbook.
      inquire xls-workbook,
             @WorkSheets::@Item(1) in xls-worksheet.
      modify xls-worksheet,
             @Name = "Asya2Xls"

      inquire xls-worksheet,
*             @Cells::@Item(1,"A") in xls-range.
              @Range("A1") in xls-range

      move all low-values     to xls-connection
      string xls-connection,
             "TEXT;" delimited by low-values
             HEDEF   delimited by spaces
             into xls-connection    
      inspect xls-connection replacing trailing spaces by low-values
      modify xls-worksheet,
             @QueryTables::@Add(by name @Connection xls-connection,
                                by name @Destination xls-range) giving xls-query. 

      perform excel-ole-set-column-type.  
 
      modify xls-query,
             @Name("ASYA_QUERY"),
             @FieldNames(1),
             @RowNumbers(0),
             @FillAdjacentFormulas(0),
             @PreserveFormatting(1),
             @RefreshOnFileOpen(0),
             @RefreshStyle(xlInsertDeleteCells),
             @SavePassword(0),
             @SaveData(1),
             @AdjustColumnWidth(0),
             @RefreshPeriod(0)
             @TextFilePromptOnRefresh(0),
             @TextFilePlatform(xlWindows),
             @TextFileStartRow(1),
             @TextFileParseType(xlDelimited),
             @TextFileTextQualifier(xlTextQualifierNone),
             @TextFileConsecutiveDelimiter(0),
             @TextFileTabDelimiter(1),
             @TextFileSemicolonDelimiter(0),
             @TextFileCommaDelimiter(0),
             @TextFileSpaceDelimiter(0),
*             @TextFileTrailingMinusNumbers(1), | excel11.def 2003 te yok
             @Refresh(by name BackgroundQuery 0).


      if runtime-major-version > 15
         modify xls-query,
                @TextFileColumnDataTypes(xls-column-type-array)
      end-if

      destroy xls-range.
      modify xls-query,
             @Delete()
      destroy xls-query.

      initialize rng-deg 
      move grid0-kisit-detay-sayi         to rng-i
      perform rng-bul.

*/Basliklari merge et ortala ve renklendir
      modify xls-app,
             @DisplayAlerts(0) | Ekrana uyari verme cell birlestirirken
             @ScreenUpdating(0)


      perform varying i
              from 1
              by 1
              until i > ole-excel-b-sayi
              move all low-values to my-range
              string my-range,
                     "A"                  delimited by low-values
                     i                    delimited by low-values
                     ":"                  delimited by low-values
                     rng-str              delimited by low-values
                     i                    delimited by low-values
                     into my-range

              inquire xls-worksheet,
                      @Range(my-range)::@Interior in xls-interior
              modify xls-interior,
                     @Color = xls-gri-renk 
              use xls-worksheet @Range(my-range)
                  modify  ^@MergeCells(1)
                          ^@HorizontalAlignment(xlHAlignCenter)
                          ^@VerticalAlignment(xlHAlignCenter)
              end-use
              destroy xls-interior
      end-perform

*/Detaylari renklendir
      perform varying i
              from 1
              by 1
              until i > ole-excel-d-sayi
              compute ii = i + ole-excel-b-sayi
              move all low-values to my-range

              string my-range,
                     "A"                  delimited by low-values
                     ii                   delimited by low-values
                     ":"                  delimited by low-values
                     rng-str              delimited by low-values
                     ii                   delimited by low-values
                     into my-range

              inquire xls-worksheet,
                      @Range(my-range)::@Interior in xls-interior
              modify xls-interior,
                     @Color = xls-mavi-renk 
              destroy xls-interior
      end-perform

*/ kare cizgileri cek
      compute to-satir = kayit-sayi
      compute from-satir = ole-excel-d-sayi + ole-excel-b-sayi 
      move all low-values to my-range
      string my-range,
             "A"         delimited by low-values
             from-satir  delimited by low-values
             ":"         delimited by low-values
             rng-str     delimited by low-values
             to-satir    delimited by low-values
             into my-range

         
      inquire xls-Worksheet, 
             @Range(my-range)::@Borders in xls-borders

      modify xls-borders(xlEdgeLeft),
             @Linestyle(xlDash)
      modify xls-borders(xlEdgeTop),
             @Linestyle(xlDash)
      modify xls-borders(xlEdgeRight),
             @Linestyle(xlDash)
      modify xls-borders(xlEdgeBottom),
             @Linestyle(xlDash).
      destroy xls-borders

*/yukar-dan a¦aÿ-ya renk
      move all low-values to my-range
      string my-range,
             "A"         delimited by low-values
             from-satir  delimited by low-values
             ":"         delimited by low-values
             "A"         delimited by low-values
             to-satir    delimited by low-values
             into my-range

      inquire xls-Worksheet, 
             @Range(my-range)::@Interior in xls-interior

      modify xls-interior,
             @Color = xls-mavi-renk 
 
      destroy xls-range xls-interior.
*/ AutoFit eyle
      modify xls-worksheet,
             @Columns::@Autofit() 

      modify xls-app,
             @ScreenUpdating(1)

      if excel-ver < 12 or csvyap-link-islem-xls-yap
         inspect hedef replacing all ".csv" by ".xls"
         modify xls-workbook,
                @SaveAs(by name @Filename hedef,
                        by name @FileFormat @xlNormal,
                        by name @Password null,
                        by name @WriteResPassword null,
                        by name @ReadOnlyRecommended 0,
                        by name @CreateBackup 0,
                        by name @AccessMode @xlNoChange)
      else
         inspect hedef replacing all low-values by spaces
         inspect hedef replacing all ".csv " by ".xlsx"
         inspect hedef replacing trailing spaces by low-values
         modify xls-workbook,
                @SaveAs(by name @Filename hedef,
                        by name @FileFormat @xlOpenXMLWorkbook ,
                        by name @Password null,
                        by name @WriteResPassword null,
                        by name @ReadOnlyRecommended 0,
                        by name @CreateBackup 0,
                        by name @AccessMode @xlNoChange)
      end-if

      modify xls-app,
             @DisplayAlerts(1)

      modify xls-workbook,
             @Close().
      destroy xls-worksheet.
      destroy xls-workbook.
      modify xls-app,
             @Quit().
      destroy xls-app.

*
 excel-ole-set-column-type.
      if runtime-major-version < 15
         exit paragraph.
      perform varying i
              from 1
              by 1
              until i > grid0-kisit-detay-sayi
              call "c$setvariant" using 2,xls-column-type(i)
      end-perform.

*
 rng-bul.
      divide rng-i
             by 25
             giving rng-bolum
             remainder rng-kalan
      move all low-values to rng-str
      if rng-bolum <> 0 
         move rng(rng-bolum:1) to rng-str(1:1)
         if rng-kalan = 0 
            move "A" to rng-str(2:1)
         end-if
      end-if
      if rng-kalan <> 0 
         string rng-str,
                rng(rng-kalan:1) delimited by low-values
                into rng-str
      end-if.

*
 yuzde-goster.
     add 1 to frame-sayac.
     if frame-sayac = frame-kayitta and frame-adet not = zeroes
        move zeroes to frame-sayac
        move frame-yuzde-pos to eframe-yuzde-pos
        modify yuzde-frm,
               title frame-frame-title
               fill-percent = frame-yuzde-pos
        add frame-ciz      to frame-yuzde-pos
        subtract frame-ciz from frame-adet
    end-if.    
*
 sirket-bilgileri.
    initialize ole-excel-bilgi
    open input isyeri.
    initialize isyeri-rec
    move isyeri-adres-tasi         to isyeri-adres
    read isyeri no lock key isyeri-adres invalid
         move "Tanimsiz ......"    to isyeri-unvan
    end-read

    move isyeri-adres               to ole-excel-bilgi
    move "-"                        to ole-excel-bilgi(9:)
    move isyeri-unvan               to ole-excel-bilgi(10:)
    close isyeri.

   
*
 zaman-tarih-bilgileri.
    initialize ole-excel-bilgi
    accept zaman from time
    accept tarih from century-date
    move yil                  to eyil
    move ay                   to eay
    move gun                  to egun
    move etarih               to ole-excel-bilgi(1:10)

    move saat                 to esaat
    move dakika               to edakika
    move saniye               to esaniye
    move ezaman               to ole-excel-bilgi(12:9)
    
    open input kllnc
    move k-kodu-tasi         to k-kodu
    read kllnc no lock invalid
         move "Tanimsiz ..." to k-adi
    end-read
    close kllnc
    move k-adi to ole-excel-bilgi(22:).
*
 fiyat-oda-ref-after. 
     
    if fiyat-ref-external> 0
      move fiyat-ref-external to ref-sat-adi
      move "-"              to ref-sat-adi(2:1)
      if fiyat-ref-external <= max-ref then
        move ref-adi(fiyat-ref-external)  to ref-sat-adi(3:)
       else
        move "????????? "  to ref-sat-adi
      end-if
      else 
       move "Tumu "  to ref-sat-adi
     end-if
    if oda-ref-external > 0
      move oda-ref-external to ref-oda-adi
      move "-"              to ref-oda-adi(2:1)
      if oda-ref-external <= max-ref then
        move ref-adi(oda-ref-external)  to ref-oda-adi(3:)
       else
        move "????????? "  to ref-oda-adi(4:)
      end-if
      else 
       move "Tumu "  to ref-oda-adi
     end-if
     
     move "Oda Ref :"      to ref-baslik-detay 
     move ref-oda-adi      to ref-baslik-detay(10:)
    
     move "Fiyat Ref :"    to ref-baslik-detay(21:) 
     move ref-sat-adi      to ref-baslik-detay(32:)
     move 0                to ref-baslik-yaz.

 
