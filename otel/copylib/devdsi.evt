* devdsi.evt
* devdsi.evt is generated from C:\asya\Acugt.Son\otel-IST\devdsi.Psf
* This is a generated file. DO NOT modify this file directly.


 form2-Exception-Proc.
     PERFORM form2-Ex-Other
     .
***   start event editor code   ***

* 
 islem.

              open i-o genelfis
              move 1 to genelfis-anahtar
              read genelfis no lock invalid
                   accept print-no from time
              not invalid
                   add 1 to print-no
                   add 1 to EKRAN-FIS-1
                   rewrite genelfis-rec end-rewrite
              end-read
              close genelfis
              move EKRAN-FIS-1 to takas-no  takas2-no takasdan-no


              initialize dokumer-rec dokumer-anah dokumer-ozellikler-rec
                         dokumer-dosya

              set  dokumer-asya-set to true
              move print-no         to dokumer-dosya-adi
              open output dokumer
*/WINDOW TITLE
              initialize dokumer-rec detay
              move "W"                    to det-dokumer-20(1:)
              move "Danisman Satis Kontrol Raporu" to det-filler
              write dokumer-rec from detay
*/ BASLIKLAR
              initialize dokumer-rec detay
              move "B"                  to det-dokumer-20(1:)
              move "Danisman Satis Kontrol Raporu" to det-filler
              write dokumer-rec from detay
              initialize dokumer-rec detay
              move "B"                  to det-dokumer-20(1:)
              write dokumer-rec from detay

*/ DOKUMER OZELLIKLER-REC
              initialize dokumer-rec detay
              move "O"                        to det-dokumer-20(1:)
              move "E" to dokumer-oto-sayfa-basi
              move 56  to dokumer-oto-sayfa-satir
              move "|" to dokumer-detay-kolon-ayirici
              move "RLRRRRRRRRRRRRRLLLLLL" to dokumer-align-occ
              move dokumer-ozellikler-rec     to det-filler
              write dokumer-rec from detay
*/ BASLIKLAR 
              initialize dokumer-rec detay
              move "D1"              to det-dokumer-20(1:)     
              move "Kodu"            to det-1
              move "Danisman Adi"    to det-2
              move "Etkl. Sts"    to det-3
              move "Brbr. Sts"    to det-4
              move "Toplm Sts"    to det-5
              move "E.Yuzde %"         to det-6
              move "B.Yuzde %"         to det-8
              move "T.Yuzde %"         to det-7
              move "B.Satis Tut."      to det-9
              move "E.Satis Tut."      to det-10
              move "T.Satis Tut."      to det-11
              move all "|" to fil-1 fil-2 fil-3 fil-4 fil-5 fil-6 fil-8 fil-7 fil-9 fil-10 fil-11
              write dokumer-rec from detay
              initialize dokumer-rec detay
              move "D2"               to det-dokumer-20(1:)     
              move "-"                to det-dokumer-20(5:1)
              move all "-" to det-1 det-2 det-3 det-4  det-5  det-6 DET-7 DET-8 det-9 det-10 det-11
              write dokumer-rec from detay 


              open output takas takas2 takasdan close takas takas2 takasdan
              open i-o  takas takasdan takas2
              open input devremulk DANISMAN danisman2 gorev donem2 odalar

                     perform toplam-hisse-sayisi-bul
                     perform toplam-danisman-sayisi-bul

                     perform danisman-etkilenen-bul
                     perform rapor-islem

                     initialize takas2-rec ydk-takas-gorev  toplam-satis-etkilenen  toplam-satis-birebir yuzde-hesapla toplam-birebir-sat-tut
                     move low-values  to takas2-gorev-sira
                     start takas2 key not < takas-sira-anah invalid
                             continue
                     not invalid
                     perform until fs-takas2 = "10"
                     read takas2 next no lock end move "10" to fs-takas2 
                     not end
                            if takas2-gorev-sira <> ydk-takas-gorev or ydk-takas-gorev = zeroes 
                                  ||BASLIK AT
                                  initialize detay  
                                  move takas2-gorev-sira to det-1
                                  move takas2-gorev-adi to det-2
                                  move "A" to det-dokumer-20(3:1)
                                  move 481 to det-renk1
                                  move "1" to det-dokumer-20(10:1)
                                  move all "|" to fil-1 fil-2 fil-3 fil-4 fil-5 fil-6 fil-8  fil-7  fil-9 fil-10 fil-11
                                  write dokumer-rec from detay 
                                  initialize xtakas-kayit-sira     
                            end-if
                            add 1 to xtakas-kayit-sira

                            if  xtakas-kayit-sira > en-cok-ilk and en-cok-ilk <> zeroes then
                                 exit perform cycle
                            end-if


                            initialize detay  
                            move takas2-danisman-kodu     to z-kodu
                            move z-kodu                  to det-1
                            move takas2-danisman-adi      to det-2 
                            move takas2-etkilenen-satis   to z-sayi
                            move z-sayi                  to det-3
                            move takas2-birebir-satis     to z-sayi
                            move z-sayi                  to det-4
                            initialize toplam-satis
                            compute toplam-satis = takas2-etkilenen-satis + takas2-birebir-satis
                            move toplam-satis            to z-sayi
                            move z-sayi                  to det-5

                            compute yuzde-hesapla ROUNDED = ( takas2-etkilenen-satis / toplam-hisse-sayisi ) * 100
                            move yuzde-hesapla           to yuzde-z
                            move yuzde-z                 to det-6

                            compute yuzde-hesapla ROUNDED = ( takas2-birebir-satis / toplam-hisse-sayisi ) * 100
                            move yuzde-hesapla           to yuzde-z
                            move yuzde-z                 to det-8

                            compute yuzde-hesapla ROUNDED = ( toplam-satis / toplam-hisse-sayisi ) * 100
                            move yuzde-hesapla           to yuzde-z
                            move yuzde-z                 to det-7

                            move takas2-birebir-satis-fiy to z-tutar
                            move z-tutar                 to det-9

                            move takas2-etkilenen-satis-fiy to z-tutar
                            move z-tutar                 to det-10

                            compute tutar-hesap = takas2-etkilenen-satis-fiy + takas2-birebir-satis-fiy
                            move tutar-hesap             to z-tutar
                            move z-tutar                 to det-11

                            add takas2-etkilenen-satis   to toplam-satis-etkilenen
                            add takas2-birebir-satis     to toplam-satis-birebir
                            add takas2-birebir-satis-fiy to toplam-birebir-sat-tut

                           if takas-pasif = 1 then
                                  move "A" to det-dokumer-20(3:1)
                                  move 176 to det-renk1
                                  move "1" to det-dokumer-20(10:1)
                            end-if


                            move all "|" to fil-1 fil-2 fil-3 fil-4 fil-5 fil-6 fil-8 fil-7 fil-9 fil-10 fil-11
                            write dokumer-rec from detay 
                            move takas2-gorev-sira to ydk-takas-gorev 
                           
                     end-read
                     end-perform
                     end-start

                ||BASLIK AT
                      initialize detay  
                      move "TOPLAM DANISMAN SATISLARINDAN"         to det-2
                      move toplam-satis-birebir    to z-sayi
                      move z-sayi                   to det-4
       
                      compute yuzde-hesapla ROUNDED = ( toplam-satis-birebir / toplam-hisse-sayisi ) * 100
                      move yuzde-hesapla           to yuzde-z
                      move yuzde-z                 to det-8

                      
                      move toplam-birebir-sat-tut to z-tutar
                      move z-tutar                to det-9

                      move "A" to det-dokumer-20(3:1)
                      move 112 to det-renk1
                      move "1" to det-dokumer-20(10:1)
                      move all "|" to fil-1 fil-2 fil-3 fil-4 fil-5 fil-6
                      write dokumer-rec from detay  
      
                      initialize detay  
                      move "KALAN HISSE SAYISI"         to det-2
                      compute toplam-satis = toplam-hisse-sayisi - toplam-satis-birebir
                      move toplam-satis            to z-sayi
                      move z-sayi                  to det-4

                      compute yuzde-hesapla ROUNDED = ( toplam-satis / toplam-hisse-sayisi ) * 100
                      move yuzde-hesapla           to yuzde-z
                      move yuzde-z                 to det-8


                      move "A" to det-dokumer-20(3:1)
                      move 481 to det-renk1
                      move "1" to det-dokumer-20(10:1)
                      move all "|" to fil-1 fil-2 fil-3 fil-4 fil-5 fil-6
                      write dokumer-rec from detay 
    
                      initialize detay  
                      move "TOPLAM HISSE SAYISI"   to det-2
                      move toplam-hisse-sayisi     to z-sayi
                      move z-sayi                  to det-4

                      move "A" to det-dokumer-20(3:1)
                      move 481 to det-renk1
                      move "1" to det-dokumer-20(10:1)
                      move all "|" to fil-1 fil-2 fil-3 fil-4 fil-5 fil-6
                      write dokumer-rec from detay  

              close dokumer
              call dokumer-prog on exception
                   perform callerr-proc
              not on exception
                   cancel dokumer-prog
              end-call
               close devremulk DANISMAN danisman2 takasdan  gorev takas  donem2 odalar takas2
              delete file dokumer takas takas2 takasdan .

*
 rapor-islem.


                          initialize takasdan-rec  
                          start takasdan key not < takasdan-anah invalid
                                  continue
                          not invalid
                          perform until fs-takasdan = "10"
                          read takasdan next no lock end move "10" to fs-takasdan 
                          not end
                                
                                   initialize danisman-rec
                                   move takasdan-kodu to danisman-kodu 
                                   read danisman no lock invalid
                                           continue
                                   end-read

                                   initialize devremulk-rec
                                   move takasdan-alt-kodu to devremulk-satan-danisman
                                   start devremulk key not < devremulk-satan-danisman invalid
                                           continue
                                   not invalid
                                   perform until fs-devremulk = "10" 
                                   read devremulk next no lock end move "10" to fs-devremulk
                                   not end
                                             if takasdan-alt-kodu <> devremulk-satan-danisman then
                                                   exit perform
                                             end-if

                                             if devremulk-durumu <> "Y" then
                                                   exit perform cycle
                                             end-if
  
                                             initialize takas-rec 
                                             move takasdan-kodu to takas-danisman-kodu
                                             read takas no lock invalid
                                                   if takasdan-alt-kodu = takasdan-kodu then
                                                         move 1 to takas-birebir-satis 
                                                         move devremulk-satis-tutari to takas-birebir-satis-fiy
                                                   else
                                                         move 1 to takas-etkilenen-satis  
                                                         move devremulk-satis-tutari to takas-etkilenen-satis-fiy
                                                   end-if
                                             not invalid
                                                     if takasdan-alt-kodu = takasdan-kodu then
                                                         add 1 to takas-birebir-satis 
                                                         add devremulk-satis-tutari to takas-birebir-satis-fiy
                                                    else
                                                         add 1 to takas-etkilenen-satis 
                                                         add devremulk-satis-tutari to takas-etkilenen-satis-fiy
                                                    end-if
                                             end-read

                                             initialize gorev-rec
                                             move danisman-gorev-kodu to gorev-kodu
                                             read gorev no lock invalid
                                                    initialize  takas-gorev-sira takas-gorev-kodu
                                                    move "Gorevi Tanimsiz Olanlar .... " to takas-gorev-adi
                                                    move 99 to takas-gorev-kodu takas-gorev-sira
                                             not invalid
                                                    move gorev-sira To takas-gorev-sira
                                                    move gorev-kodu to takas-gorev-kodu
                                                    move gorev-adi  to takas-gorev-adi
                                             end-read

                                             initialize string-deger 
                                             string danisman-adi delimited by "   "
                                                   " " delimited by size
                                                   danisman-soyadi  delimited by "   "
                                                   " " delimited by size
                                              into string-deger
                                              move string-deger to takas-danisman-adi
                                              move danisman-pasif to takas-pasif

                                              write takas-rec invalid
                                                  rewrite takas-rec invalid
                                                           stop " "
                                                  end-rewrite   
                                              end-write
        
                                             initialize takas-birebir-satis takas-etkilenen-satis
                                   end-read
                                   end-perform
                                   end-start

                          end-read
                          end-perform
                          end-start

            initialize takas-rec sira-no
            if siralama-turu(1:2) = "01" then
                 move high-values to takas-birebir-satis 
                 start takas key not > takas-birebir-satis invalid
                     continue
                 end-start
            else 
                 move high-values to takas-etkilenen-satis 
                 start takas key not > takas-etkilenen-satis invalid
                     continue
                 end-start
            end-if
            
            perform until fs-takas = "10"
            read takas previous no lock end move "10" to fs-takas
            not end
                   add 1 to sira-no
                   initialize takas2-rec
                   move takas-rec to takas2-rec
                   move sira-no to takas2-sira-no
                   write takas2-rec invalid
                           continue
                   end-write
            end-read
            end-perform



         .
*
 danisman-etkilenen-bul.                 
          ||||BUNU HANGI KAFAYLA YAPYIYSAM O KAFA BIR DAHA GELMEZZ =P S.Y |||||
          
          initialize danisman-rec danisman-sayi
          start danisman key not < danisman-anah invalid
                  continue
          not invalid
          perform until fs-danisman = "10"
          read danisman next no lock end move "10" to fs-danisman
          not end

                  if danisman-pasif = 1 and pasif-dahil <> 1 then
                        exit perform cycle
                  end-if
                  add 1 to danisman-sayi
                  initialize siradaki
                  initialize takasdan-kodu 
                  move danisman-kodu  to takasdan-kodu takasdan-alt-kodu
                  write takasdan-rec invalid 
                         stop " "
                  end-write

                   |||||  ALT DANISMANLARI BULLL |||||||||||||||||||||||||||||||||||||||||||
                  initialize ust-dongu-cikti  danis-dizi-sira a-kayit p-kayit 
                  move danisman-kodu to siradaki-danisman-kodu(1) 
                  move 1 to danis-dizi-sira i
                  perform varying i from 1 by 1 until i > 9999

                        if siradaki-danisman-kodu(i) = zeroes or spaces then
                              exit perform 
                        end-if
                        initialize  fs-danisman2
                        move siradaki-danisman-kodu(i) to danisman2-bagli-kodu  
                        start danisman2 key not < danisman2-bagli-kodu invalid
                                move 1 to ust-dongu-cikti
                                continue
                        not invalid
                        perform until fs-danisman2 = "10" 
                        read danisman2 next no lock end move "10" to fs-danisman2
                        not end
        
                               if danisman2-bagli-kodu not = 
                               siradaki-danisman-kodu(i) then
                                     exit perform
                               end-if

                               initialize takasdan-rec
                               move danisman-kodu    to takasdan-kodu
                               move danisman2-kodu   to takasdan-alt-kodu

                               write takasdan-rec invalid 
                                      rewrite takasdan-rec invalid 
                                           stop " "
                                      end-rewrite
                               end-write


                               add 1 to danis-dizi-sira
                               move danisman2-kodu to
                               siradaki-danisman-kodu(danis-dizi-sira) 
                        end-read
                        end-perform
                        end-start
                  end-perform

                  compute yuzde-hesapla ROUNDED = ( danisman-sayi / toplam-danisman-sayisi ) * 100
                  modify bar-sc , size = (yuzde-hesapla * 0.26 )
                  move yuzde-hesapla to bar-yuzde-deger 
                  display bar-sc
          
          end-read
          end-perform
          end-start

         
          |||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||-

          .
*
 form2-Bef-Create.
       perform adresleri-tasi
     .
*
 form2-Ex-Other.
      evaluate key-status
          when 2

             perform islem
      end-evaluate
     .
*
 toplam-danisman-sayisi-bul.
          initialize danisman-rec toplam-danisman-sayisi
          start danisman key not < danisman-anah invalid
                  continue
          not invalid
          perform until fs-danisman = "10"
          read danisman next no lock end move "10" to fs-danisman
          not end
                 if danisman-pasif = 1 and pasif-dahil <> 1 then
                        exit perform cycle
                 end-if
                 add 1 to toplam-danisman-sayisi
          end-read
          end-perform
          end-start
              .
*
 toplam-hisse-sayisi-bul.
 
         initialize odalar-rec toplam-hisse-sayisi
         start odalar key not < odalar-anah invalid
                 continue
         not invalid
         perform until fs-odalar = "10" 
         read odalar next no lock end move "10" to fs-odalar
         not end
               if ODALAR-HAYALI = "H" then
                     exit perform cycle
               end-if
               if odalar-devre = spaces then
                     exit perform cycle
               end-if

               initialize donem2-rec
               move odalar-devre to DONEM2-KODU(1:1)
               start donem2 key not < donem2-anah invalid
                 continue
               not invalid
               perform until fs-donem2 = "10" 
               read donem2 next no lock end move "10" to fs-donem2
               not end
                       if donem2-kodu(1:1) <>  odalar-devre
                             exit perform
                       end-if

                       add 1 to toplam-hisse-sayisi
               end-read
               end-perform
               end-start

         end-read
         end-perform
         end-start

         .

 
