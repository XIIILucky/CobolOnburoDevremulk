* limit.evt
* limit.evt is generated from D:\asya\acugt.ytl\otel\limit.Psf
* This is a generated file. DO NOT modify this file directly.


 Form1-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 cb-kart-tipi-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-oda-no-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-folio-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-kart-no-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-limit-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-eklenecek-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-toplam-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 Pb-oda-no-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 Pb-folio-no-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 Pb-kart-no-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-adi-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-soyadi-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-gir-gun-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-gir-ay-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-gir-yil-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-cik-gun-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-cik-ay-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-cik-yil-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-buyuk-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-kucuk-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-free-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 Cb-Limit-kontrol-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 cb-alkol-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 acc-depart-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 Pb-depart-Exception-Proc.
     PERFORM Exception-Procedurler
     .

 Pb-Kaydet-Exception-Proc.
     PERFORM Exception-Procedurler
     .
***   start event editor code   ***
*
 Form1-Bef-Create.
    perform adresleri-tasi
    open i-o odalar2.
    open input genel
    move 1 to genel-anahtar
    read genel no lock
      invalid
        move 3 to mesaj-no
        perform mesaj-ver
        set exit-pushed to true
      not invalid
*       Departman kodu degistirilirse ilk hali burada
        move onkpara-depozit    to rapor-dep-kodu
        move cost-sirketi of genel to cstgenel-dosya-alan
        open input cstgenel
        move 1 to cstgenel-anahtar
        read cstgenel no lock
          invalid
            move 3 to mesaj-no
            perform mesaj-ver
            set exit-pushed to true
          not invalid
            move 3 to kart-alkol
        end-read
        close cstgenel
    end-read
    close genel
    .
*
 Form1-Aft-Create.
     perform ekran-goster
     perform ersan-ekleme
     .
*
 Form1-Aft-Initdata.
*    move tarih-tasi to dov-boz-tarih dov-boz-belge-tar.
*/-----------------------------\*  
*/---------DIL AYARLA----------\*    
*/-----------------------------\*    
     perform dil-ayarla-proc.
*/-----------------------------\*         
    .
*
 Exception-Procedurler.
    evaluate key-status
      when 1
        evaluate control-id
          when 201
          when 211 perform ara-oda-no
          when 202
          when 212 perform ara-folio-no
          when 203
          when 213 perform ara-kart-no
          when 3014
          when 3114 perform ara-depart
        end-evaluate
      when 2
        perform limit-kaydet-kontrol
    end-evaluate
    .
*
 ekran-goster.
     if musteri
        modify acc-adi,     ENABLED   = false,
        modify acc-soyadi,  ENABLED   = false,
        modify acc-gir-gun, ENABLED   = false,
        modify acc-gir-ay,  ENABLED   = false,
        modify acc-gir-yil  ENABLED   = false,
        modify acc-cik-gun, ENABLED   = false,
        modify acc-cik-ay,  ENABLED   = false,
        modify acc-cik-yil  ENABLED   = false,
     else
        modify acc-adi,     ENABLED,  = true,
        modify acc-soyadi,  ENABLED   = true,
        modify acc-gir-gun, ENABLED   = true,
        modify acc-gir-ay,  ENABLED   = true,
        modify acc-gir-yil, ENABLED   = true
        modify acc-cik-gun, ENABLED   = true
        modify acc-cik-ay,  ENABLED   = true
        modify acc-cik-yil, ENABLED   = true
     end-if
     display acc-oda-no, acc-folio, acc-kart-no,
             acc-limit, acc-eklenecek, acc-toplam,
             acc-adi, acc-soyadi, acc-gir-gun,
             acc-gir-ay, acc-gir-yil, acc-cik-gun,
             acc-cik-ay, acc-cik-yil, acc-buyuk,
             acc-kucuk, acc-free, cb-limit-kontrol,
             cb-alkol, acc-depart acc-indirim, lb-board
     perform alkol-goster
     perform limit-kontrol-goster
     perform depart-goster
     .
 ekran-temizle.
     initialize rapor-kart-no, rapor-limit, rapor-eklenecek
                rapor-toplam, rapor-adi,
                rapor-soyadi, rapor-giris-tarih,
                rapor-cikis-tarih, rapor-kisi-sayilari, rapor-indirim
     move onkpara-depozit to rapor-dep-kodu
     .
*
 alkol-goster.
     modify cb-alkol, VALUE = kart-alkol
     display cb-alkol
     .
*
 limit-kontrol-goster.
     modify Cb-Limit-kontrol, VALUE = rapor-limit-kontrol
     display Cb-Limit-kontrol
     .
*
 depart-goster.
     perform Hata-Kontrol-Depart
     if hata-var
        set hata-yok to true
        modify La-depozit-adi, TITLE = "Tanimsiz Departman"
     else
        modify La-depozit-adi, TITLE = depkod-adi
     end-if
     display acc-depart la-depozit-adi
     .
*
 ersan-ekleme.
     if genel-ersan-eh = "E" 
        if 1 > genel-ersan-bp or genel-ersan-bp > 16
           move 1 to bp      
        else
           move genel-ersan-bp to bp
        end-if
        if 1 > genel-ersan-au or genel-ersan-au > 16
           move 7 to au      
        else
           move genel-ersan-au to au
        end-if
        initialize tus
        compute tus = (bp + au) - 1
        modify acc-kart-no, max-text = tus
     else
        move 1 to bp 
        move 7 to au
        modify acc-kart-no, max-text = 7
     end-if
     .
*
 ara-oda-no.
     initialize oda-cagir
     move "D" to oda-db-cagir
     move rapor-oda-no   to o-uzun
            perform oda-kisalt
            move o-kisa to    odano-cagir
     call "/asya/ytl/obj/otel/odaara.asy"
       using oda-cagir
       on exception
         perform callerr-proc
       not on exception
         cancel "/asya/ytl/obj/otel/odaara.asy"
         initialize rapor-oda-no
         move odano-cagir to o-kisa 
            perform oda-uzat
            move o-uzun to  rapor-oda-no
         move odano-cagir to folara-odano
         display acc-oda-no
     end-call
     .
*
 ara-folio-no.
     if musteri
        initialize folara-cagir
        move rapor-oda-no to o-uzun
            perform oda-kisalt
            move o-kisa to folara-odano
        move rapor-folio  to folara-no-cagir
        move "I" to folara-cagri-tipi
        call "/asya/ytl/obj/otel/folara.asy"
          using folara-cagir
          on exception
            perform callerr-proc
          not on exception
            cancel "/asya/ytl/obj/otel/folara.asy"
            initialize rapor-folio
            move folara-no-cagir to rapor-folio
            display acc-folio
        end-call
     else
        initialize odenmez-cagir
        move "O" to oden-ilk
        move rapor-folio to oden-kod
        call "/asya/ytl/obj/otel/odenara.asy"
          using odenmez-cagir
          on exception
            perform callerr-proc
          not on exception
            cancel "/asya/ytl/obj/otel/odenara.asy"
            initialize rapor-folio
            move oden-kod to rapor-folio
            display acc-folio
        end-call
     end-if
     .
*
 ara-kart-no.
     initialize kartara-cagir
     move rapor-oda-no to o-uzun
            perform oda-kisalt
            move o-kisa to kartara-oda
     move rapor-folio  to kartara-folio
     move rapor-kart-no(bp:au) to kartara-kart
     call "/asya/ytl/obj/otel/kartara.asy"
       using kartara-cagir
       on exception
         perform callerr-proc
       not on exception
         cancel "/asya/ytl/obj/otel/kartara.asy"
         move all "*" to rapor-kart-no(bp:au)
         move kartara-kart to rapor-kart-no(bp:au)
         display acc-kart-no
     end-call
     .
*
 ara-depart.
     initialize depkod-cagir
     move "A" to depkod-cagri-tipi
     move rapor-dep-kodu to depkod-dep-kodu
     call "/asya/ytl/obj/otel/depara.asy"
       using depkod-cagir
       on exception
         perform callerr-proc
       not on exception
         cancel "/asya/ytl/obj/otel/depara.asy"
         move depkod-dep-kodu to rapor-dep-kodu
         display acc-depart
     end-call
     .
*
 Before-Procedurler.
     initialize event-control-id
     initialize mesaj
     evaluate true
       when ingilizce
       when almanca
       when turkce
         evaluate control-id
           when 101
             initialize rapor
             perform Ekran-Goster
             move "Kart Tipi Secin" to mesaj
           when 201
           when 211
             if odenmez
                move 4   to accept-control
                move 202 to control-id
             else
                initialize rapor-folio
                perform Ekran-Temizle
                perform Ekran-Goster
                move "Oda Numarasi Secin..." to mesaj
             end-if
           when 202
           when 212
             perform Ekran-Temizle
             perform Ekran-Goster
             if musteri
                move "Folio Numarasi Secin..." to mesaj
                if (rapor-folio = spaces or rapor-folio = zeroes)
                   perform Ara-Folio-No
                end-if
             else
                if onkpara-kart-odenmez-yarat = "E"
                   move 4   to accept-control
                   move 203 to control-id
                else
                   move "Odenmez Numarasi Secin..." to mesaj
*                   if (rapor-folio = spaces or rapor-folio = zeroes)
*                      perform Ara-Folio-No
*                   end-if
                end-if
             end-if
           when 203
           when 213
             move "Odaya Ait Kart Numarasi Secin..." to mesaj
*             if (rapor-kart-no = spaces or rapor-kart-no = zeroes)
*                perform Ara-Kart-No
*             end-if
           when 234
             move "Gecerli Limit..." to mesaj
           when 205
             move "Eklenecek Limiti Girin..." to mesaj
           when 236
             move "Toplam Limit..." to mesaj
           when 301
             move "Odenmez/Musteri Adi Girin..." to mesaj
           when 302
             move "Odenmez/Musteri Soyadi Girin..." to mesaj
           when 303
             move "Kartin Ilk Kullanma Gununu Girin..." to mesaj
           when 304
             move "Kartin Ilk Kullanma Ayini Girin..." to mesaj
           when 305
             move "Kartin Ilk Kullanma Yilini Girin..." to mesaj
           when 306
             move "Kartin Son Kullanma Gununu Girin..." to mesaj
           when 307
             move "Kartin Son Kullanma Ayini Girin..." to mesaj
           when 308
             move "Kartin Son Kullanma Yilini Girin..." to mesaj
           when 309
             move "Yetiskin Sayisi Girin..." to mesaj
           when 3010
             move "Cocuk Sayisi Girin..." to mesaj
           when 3011
             move "Free Kisi Sayisi Girin..." to mesaj
           when 3012
             move "Limit Bittiginde Uyari Var/Yok..." to mesaj
           when 3013
             move "Alkol Seviyesini Secin..." to mesaj
           when 3014
             move "Depozit Islenecek Departman Secin..." to mesaj
             if (rapor-dep-kodu = spaces or rapor-dep-kodu = zeroes)
                perform ara-depart
             end-if
           end-evaluate
     end-evaluate
     modify Form1-Sb-Handle,
       panel-index  = 2
       panel-text   = mesaj
    .
*
 After-Procedurler.
     move key-status   to tus
     initialize key-status
     evaluate control-id
       when 101 perform after-kart-tipi
       when 201
       when 212 perform after-oda-no
       when 202
       when 212 perform after-folio-no
       when 203
       when 213 perform after-kart-no
       when 205 perform after-limit
       when 303 perform after-gir-gun
       when 304 perform after-gir-ay
       when 305 perform after-gir-yil
       when 306 perform after-cik-gun
       when 307 perform after-cik-ay
       when 308 perform after-cik-yil
       when 3012 perform after-limit-kontrol
       when 3013 perform after-alkol
       when 3014
       when 3114 perform after-depart
     end-evaluate
     initialize Tus
     .
*
 after-kart-tipi.
     if musteri
        move 1 to rapor-limit-kontrol
        perform kodlar02-alkol-kontrol
        move kodlar02-alkol   to kart-alkol
        modify La-folio-no, title = "Folio :"
*        modify acc-oda-no,  enabled = true
*        modify Pb-oda-no,   enabled = true
        modify acc-folio,   enabled = true
        modify Pb-folio-no, enabled = true
        set odenmez-no-arttir to true
     else
        if odenmez
           move 0 to rapor-limit-kontrol
        end-if
        move 0 to kart-alkol
        modify La-folio-no, title = "Odenmez :"
        set odenmez-no-arttir to true
        if onkpara-kart-odenmez-yarat = "E"
           if odenmez-no-arttir
              perform odenmez-no-ekle
           end-if
*           modify acc-oda-no,  enabled = false
*           modify Pb-oda-no,   enabled = false
           modify acc-folio  , enabled = false
           modify Pb-folio-no, enabled = false
        end-if
     end-if
     perform Limit-Kontrol-Goster
     perform alkol-goster
     move 4   to accept-control
     move 201 to control-id
     .
*
 after-oda-no.
     if event-control-id not = 101 and
        event-control-id not = 211 and
        not Tus-geri

        perform Hata-Kontrol-Oda-No
        if Hata-Var
           set Hata-Yok to true
           perform Hata-goster-oda-no
        else
           if onkpara-kart-odenmez-yarat = "E" and
              not musteri
              move 4   to accept-control
              move 203 to control-id
           else
              move 4   to accept-control
              move 202 to control-id
           end-if
        end-if
     end-if
     .
*
 after-folio-no.
     if event-control-id not = 101 and
        event-control-id not = 201 and
        event-control-id not = 211 and
        event-control-id not = 212 and
        not Tus-geri

        if musteri
           perform Hata-Kontrol-Folio
           if Hata-Var
              set Hata-Yok to true
              perform hata-goster-folio
           else
              perform Hata-Kontrol-Konuk-Durumu
              if Hata-Var
                 set Hata-Yok to true
                 perform hata-goster-konuk-durumu
              else
                 perform Hata-Kontrol-Odeme-Tipi
                 if Hata-Var
                    set Hata-Yok to true
                    perform Hata-goster-Odeme-Tipi
                 else
                    perform Hata-Kontrol-Posting
                    if Hata-Var
                       set Hata-Yok to true
                       perform Hata-goster-Posting
                    else
                       move 4   to accept-control
                       move 203 to control-id
                    end-if
                 end-if
              end-if
           end-if
        else
           if onkpara-kart-odenmez-yarat not = "E"
              perform Hata-Kontrol-Odenmez
              if Hata-Var
                 set Hata-Yok to true
                 perform Hata-goster-Odenmez
              else
                 move 4   to accept-control
                 move 203 to control-id
              end-if
           end-if
        end-if
     end-if
     .
*
 after-kart-no.
     if event-control-id not = 101 and
        event-control-id not = 201 and
        event-control-id not = 211 and
        event-control-id not = 202 and
        event-control-id not = 212 and
        event-control-id not = 213 and
        not Tus-geri
        perform hata-kontrol-kart-bos
        if Hata-Var
           set Hata-Yok to true
           perform hata-goster-kart-bos
        else
           perform hata-kontrol-kart-no
           if Kart-Yok
              perform bilgi-aktar-bos-kart
           else
              if kart-limit-uyari = "E"
                 move 1 to rapor-limit-kontrol
              else
                 move 0 to rapor-limit-kontrol
              end-if
              perform hata-kontrol-kart-folio
              if Hata-Var
                 set Hata-Yok to true
                 perform hata-goster-kart-folio
              else
                 perform hata-kontrol-kart-tipi
                 if Hata-Var
                    set Hata-Yok to true
                    perform hata-goster-kart-tipi
                 else
                    if (kart-odenmez or kart-ikram)
                       move kart-kim to odenmez-anah
                       open input odenmez
                       read odenmez record no lock
                         key is odenmez-anah
                         invalid
                           move "Tanimsiz "  to odenmez-adi(1:20)
                           move "Odenmez"    to odenmez-adi(21:)
                       end-read
                       close odenmez
                       perform Bilgi-Aktar-Odenmez
                       move kart-tutar   to onceki-limit
                                            rapor-limit
                       move kart-cin to rapor-giris-tarih
                       move kart-cot to rapor-cikis-tarih
                    else
                       perform Bilgi-Aktar-Konuk
                       if genel-limit-karttan
                          move kart-tutar   to onceki-limit
                                               rapor-limit
                       else
                          perform limit-bul
                          move onceki-limit to rapor-limit
                       end-if
                    end-if
                    perform Bilgi-Aktar-Kart
                 end-if
              end-if
           end-if
           perform Ekran-Goster
           perform after-limit
        end-if
     end-if
     .
*
 after-limit.
     move rapor-eklenecek to eklenecek-limit
     compute toplam-limit = onceki-limit + eklenecek-limit
     move toplam-limit to rapor-toplam
     display acc-toplam
     .
*
 after-gir-gun.
     if 01 > rapor-giris-gun or rapor-giris-gun > 31
        move zeroes to rapor-giris-gun
        display acc-gir-gun
        move 4   to accept-control
        move 303 to control-id
     end-if
     .
*
 after-gir-ay.
     if 01 > rapor-giris-ay or rapor-giris-ay > 12
        move zeroes to rapor-giris-ay
        display acc-gir-ay
        move 4   to accept-control
        move 304 to control-id
     end-if
     .
*
 after-gir-yil.
     if 2000 > rapor-giris-yil
        move zeroes to rapor-giris-yil
        display acc-gir-yil
        move 4   to accept-control
        move 305 to control-id
     else
        move rapor-giris-tarih to takvim-anah
        perform hata-kontrol-tarih
        if Hata-Var
           set Hata-Yok to true
           perform hata-goster-tarih
           move 4   to accept-control
           move 303 to control-id
        end-if
     end-if
     .
*
 after-cik-gun.
     if 01 > rapor-cikis-gun or rapor-cikis-gun > 31
        move zeroes to rapor-cikis-gun
        display acc-cik-gun
        move 4   to accept-control
        move 306 to control-id
     end-if
     .
*
 after-cik-ay.
     if 01 > rapor-cikis-ay or rapor-cikis-ay > 12
        move zeroes to rapor-cikis-ay
        display acc-cik-ay
        move 4   to accept-control
        move 307 to control-id
     end-if
     .
*
 after-cik-yil.
    if 2000 > rapor-cikis-yil
        move zeroes to rapor-cikis-yil
        display acc-cik-yil
        move 4   to accept-control
        move 308 to control-id
     else
        move rapor-cikis-tarih to takvim-anah
        perform hata-kontrol-tarih
        if Hata-Var
           set Hata-Yok to true
           perform hata-goster-tarih
           move 4   to accept-control
           move 306 to control-id
        end-if
     end-if
     .
*
 after-limit-kontrol.
     if rapor-limit-kontrol = 1
        move "E" to kart-limit-uyari
     else
        move "H" to kart-limit-uyari
     end-if
     .
*
 after-alkol.
     .
*
 after-depart.
     perform hata-kontrol-depart
     if Hata-Var
        set Hata-Yok to true
        perform hata-goster-depart
     else
        perform hata-kontrol-depart-ba
        if Hata-Var
           set Hata-Yok to true
           perform hata-goster-depart-ba
        end-if
     end-if
     perform Depart-Goster 
     .
*
 odenmez-no-ekle.
     initialize rapor-folio
     open i-o genelfis
     move 1 to genelfis-anahtar
     read genelfis no lock
       invalid
         initialize genelfis-rec
         move 1 to genelfis-anahtar
     end-read
     add 1 to pos-oto-odenmez-no
     move pos-oto-odenmez-no to rapor-folio
     rewrite genelfis-rec
       invalid
         write genelfis-rec, end-write
     end-rewrite
     close genelfis
     display acc-folio
     set odenmez-no-arttirma to true
     .
*
 hata-kontrol-oda-no.
     open input odalar
     initialize odalar-rec
     move rapor-oda-no to o-uzun
            perform oda-kisalt
            move o-kisa to odalar-no
     read odalar record no lock
       key is odalar-anah
       invalid key
         set Hata-Var to true
     end-read
     close odalar
     .
*
 hata-goster-oda-no.
     display
       message box "Oda No tanimsiz" new-line
                   "Lutfen Oda Seciniz"
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     initialize rapor-oda-no
     display acc-oda-no
     move 4   to accept-control
     move 201 to control-id
     perform ara-oda-no
     .
*
 hata-kontrol-folio.
     open input konuk kodlar02
     initialize konuk-rec
     move rapor-folio to hrkgir-folio
                         konuk-folio
     read konuk record no lock
       key is konuk-anah
       invalid key
         set Hata-Var to true
     end-read
     initialize kodlar02-rec
     move "A"             to kodlar02-tipi
     move konuk-pan-tipi  to kodlar02-kodu
     read kodlar02 no lock invalid
          continue
     end-read
     close konuk kodlar02
     .
*
 hata-goster-folio.
     display
       message box "Folio tanimsiz" new-line
                   "Lutfen Folio Secin..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     initialize rapor-folio
     display acc-folio
     move 4   to accept-control
     move 202 to control-id
     .
*
 hata-kontrol-odenmez.
     move cost-sirketi of genel to Odenmez-Dosya-Adres
     open input odenmez
     initialize odenmez-rec
     move "O"         to odenmez-ilk
     move rapor-folio to odenmez-kodu
     read odenmez record no lock
       invalid key
         set Hata-Var to true
     end-read
     close odenmez
     .
*
 hata-goster-odenmez.
     display
       message box "Odenmez tanimsiz" new-line
                   "Lutfen Odenmez Secin.."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     initialize rapor-folio
     display acc-folio
     move 4   to accept-control
     move 202 to control-id
     .
*
 hata-kontrol-konuk-durumu.
     open input konuk
     initialize konuk-rec
     move rapor-folio to hrkgir-folio
                         konuk-folio
     read konuk record no lock
       key is konuk-anah
       invalid key
         set Hata-Var to true
       not invalid key
         if konuk-durumu not = "I"
            set Hata-Var to true
         end-if
     end-read
     close konuk
     .
*
 hata-goster-konuk-durumu.
     display
       message box "Folio C/Out Olmus" new-line
                   "Limit Verilemez..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     move 4   to accept-control
     move 202 to control-id
     .
*
 hata-kontrol-odeme-tipi.
     open input kodlar02
     initialize kodlar02-rec
     move "B"              to kodlar02-tipi
     move konuk-odeme-tipi to kodlar02-kodu
     read kodlar02 record no lock
       key is kodlar02-anah
       invalid key
         set Hata-Var to true
     end-read
     close kodlar02
     .
*
 hata-goster-odeme-tipi.
     display
       message box "Folionun Odeme Kodu Tanimsiz" new-line
                   "Limit Verilemez..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     move 4   to accept-control
     move 202 to control-id
     .
*
 hata-kontrol-posting.
     open input kodlar02
     initialize kodlar02-rec
     move "B"              to kodlar02-tipi
     move konuk-odeme-tipi to kodlar02-kodu
     read kodlar02 record no lock
       key is kodlar02-anah
       invalid key
         set Hata-Var to true
       not invalid key
         if ode-posting = "H" and musteri
            set Hata-Var to true
         end-if
     end-read
     close kodlar02
     .
*
 hata-goster-posting.
     display
       message box "Sectiginiz Folio ile Giris Yapmak" new-line
                   "Istediginiz Tip Uyumsuz..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     move 4   to accept-control
     move 202 to control-id
     .
*
 hata-kontrol-kart-bos.
     if rapor-kart-no = spaces or
        rapor-kart-no = zeroes
        set hata-var to true
     end-if
     .
*
 hata-goster-kart-bos.
     display
       message box "Kart Numarasi Bos Gecilemez" new-line
                   "Kart Numarasi Girin..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     move 4   to accept-control
     move 203 to control-id
     .
*
 hata-kontrol-kart-no.
     move cost-sirketi of genel to kart-Dosya-Adres
     open input kart
     initialize kart-rec
     move rapor-kart-no(bp:au) to kart-no
     read kart record no lock
       key is kart-no
       invalid key
         set Kart-yok to true
       not invalid key
         set Kart-var to true
     end-read
     close kart
     .
*
 hata-kontrol-kart-folio.
     if kart-folio not = rapor-folio
        set Hata-Var to true
     end-if
     .
*
 hata-goster-kart-folio.
     display
       message box "Kart Baska Odaya Ait  !!!" new-line
                   "Bu Odadan Limit Verilemez..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     move 4   to accept-control
     move 202 to control-id
     .
*
 hata-kontrol-kart-tipi.
     if kart-tipi not = islem-tipi
        set Hata-Var to true
     end-if
     .
*
 hata-goster-kart-tipi.
     display
       message box "Kart Tipi ile Islem Tipiniz" new-line
                   "Farkli. Limit Verilemez..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     move 4   to accept-control
     move 202 to control-id
     .
*
 hata-kontrol-tarih.
     open input takvim
     read takvim record no lock
       key is takvim-anah
       invalid
         set Hata-Var to true
     end-read
     close takvim
     .
*
 hata-goster-tarih.
     display
       message box "Tarih Tanimsiz" new-line
                   "Tarihi Kontrol Edin..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     .
*
 hata-kontrol-depart.
     open input depkod
     move rapor-dep-kodu to depkod-kodu
     read depkod record no lock
       key is depkod-anah
       invalid
         set Hata-Var to true
     end-read
     close depkod
     .
*
 hata-goster-depart.
     display
       message box "Departman Kodu Bulunamadi..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     move 4    to accept-control
     move 3014 to control-id
     .
*
 hata-kontrol-depart-ba.
     if depkod-ba not = "A"
        set Hata-Var to true
     end-if
     .
*
 hata-goster-depart-ba.
     display
       message box "Departman Kodu Bulunamadi..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     move 4    to accept-control
     move 3014 to control-id
     .
*
 hata-goster-folio-yazilamadi.
     display
       message box "Folio'ya Kayit Yazilamadi" new-line
                   "Tekrar Deneyin..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     move 4   to accept-control
     move 101 to control-id
     .
*
 hata-goster-kart-tanitilamadi.
     display
       message box "Folio'ya Limit Verildi" new-line
                   "Kart Tanitilamadi..."
       title   = "HATA"
       type    = MB-OK
       icon    = MB-ERROR-ICON
       default = MB-OK
     move 4   to accept-control
     move 101 to control-id
     .
*
 bilgi-aktar-bos-kart.
     initialize rapor-adi, rapor-soyadi,
                rapor-kisi-sayilari
     move Calisma-Tarihi to rapor-giris-tarih
                            rapor-cikis-tarih
     move 0 to rapor-limit-kontrol
     if odenmez
        perform Hata-Kontrol-Odenmez
        if Hata-Var
           set Hata-Yok to true
           move all "*" to odenmez-adi
        end-if
        perform Bilgi-Aktar-Odenmez
        move "0" to kart-alkol
     else
        if musteri
           move 1 to rapor-limit-kontrol
        end-if
        perform Bilgi-Aktar-Konuk
        perform kodlar02-alkol-kontrol
     end-if
     .
*
 bilgi-aktar-odenmez.
     move odenmez-adi(1:20) to rapor-adi
     move odenmez-adi(21:)  to rapor-soyadi
     .
*
 bilgi-aktar-konuk.
     move konuk-adi     to rapor-adi
     move konuk-soyadi  to rapor-soyadi
     move konuk-gel-tar to rapor-giris-tarih
     move konuk-git-tar to rapor-cikis-tarih
     move konuk-buyuk   to rapor-buyuk
     move konuk-kucuk   to rapor-kucuk
     move konuk-free    to rapor-free
     .
*
 bilgi-aktar-kart.
     move kart-buyuk   to rapor-buyuk
     move kart-kucuk   to rapor-kucuk
     move kart-free    to rapor-free
     move kart-ind     to rapor-indirim
     .
*
 limit-bul.
     if konuk-fol-kodu = "R"
        initialize romhrk-rec, fs-romhrk
                   onceki-limit
                   onceki-limit-dv
        move rapor-folio to romhrk-folio
                            hrkgir-folio
        open input romhrk
        start romhrk key >= romhrk-anah
          invalid
            continue
          not invalid
            perform with test after
                    until fs-romhrk = "10"
              read romhrk next no lock
                end
                  move "10" to fs-romhrk
                not end
                  if romhrk-folio = hrkgir-folio
                     if romhrk-ba = "B"
                        compute onceki-limit    = onceki-limit    - romhrk-tl-tutar
                        compute onceki-limit-dv = onceki-limit-dv - romhrk-dv-tutar
                     else
                        compute onceki-limit    = onceki-limit    + romhrk-tl-tutar
                        compute onceki-limit-dv = onceki-limit-dv + romhrk-dv-tutar
                     end-if
                  else
                     move "10" to fs-romhrk
                  end-if
              end-read
            end-perform
        end-start
        close romhrk
     else
        initialize exthrk-rec, fs-exthrk
                   onceki-limit
                   onceki-limit-dv
        move rapor-folio to exthrk-folio
                            hrkgir-folio
        open input exthrk
        start exthrk key >= exthrk-anah
          invalid
            continue
          not invalid
            perform with test after
                    until fs-exthrk = "10"
              read exthrk next no lock
                end
                  move "10" to fs-exthrk
                not end
                  if exthrk-folio = hrkgir-folio
                     if exthrk-ba = "B"
                        compute onceki-limit    = onceki-limit    - exthrk-tl-tutar
                        compute onceki-limit-dv = onceki-limit-dv - exthrk-dv-tutar
                     else
                        compute onceki-limit    = onceki-limit    + exthrk-tl-tutar
                        compute onceki-limit-dv = onceki-limit-dv + exthrk-dv-tutar
                     end-if
                  else
                     move "10" to fs-exthrk
                  end-if
              end-read
            end-perform
        end-start
        close exthrk
     end-if
     .
*
 limit-kaydet-kontrol.
     perform kayit-son-kontrol
     if Hata-Yok
        display
          message box "Bu Kayit islensin mi?"
          title   = "Kayit Onayi"
          type    = MB-YES-NO
          icon    = MB-DEFAULT-ICON
          default = MB-YES
          giving  tus

        if tus = MB-YES
           perform limit-kaydet
        else
           move 4    to accept-control
           move 3014 to control-id
        end-if
     else
        set Hata-Yok to true
     end-if
     .
*
 kayit-son-kontrol.
     if not odenmez
        perform hata-kontrol-oda-no
     end-if
     if Hata-Var
        perform hata-goster-oda-no
     else
        if musteri
           perform Hata-Kontrol-Folio
        else
           if onkpara-kart-odenmez-yarat = "E"
              if odenmez-no-arttir
                 perform odenmez-no-ekle
              end-if
           else
              perform hata-kontrol-odenmez
           end-if
        end-if
        if Hata-Var
           if musteri
              perform hata-goster-folio
           else
              perform hata-goster-odenmez
           end-if
        else
           if musteri
              perform Hata-Kontrol-Konuk-Durumu
           end-if
           if Hata-Var
              perform hata-goster-konuk-durumu
           else
              if musteri




                 perform Hata-Kontrol-Odeme-Tipi
              end-if
              if Hata-Var
                 perform Hata-goster-Odeme-Tipi
              else
                 if musteri
                    perform Hata-Kontrol-Posting
                 end-if
                 if Hata-Var
                    perform Hata-goster-Posting
                 else
                    perform hata-kontrol-kart-bos
                    if Hata-Var
                       perform hata-goster-kart-bos
                    else
                       perform hata-kontrol-kart-no
                       if Kart-Var
                          perform hata-kontrol-kart-folio
                       end-if
                       if Hata-Var
                          perform hata-goster-kart-folio
                       else
                          if Kart-Var
                             perform hata-kontrol-kart-tipi
                          end-if
                          if Hata-Var
                             perform hata-goster-kart-tipi
                          else
                             move rapor-giris-tarih to takvim-anah
                             perform hata-kontrol-tarih
                             if Hata-Var
                                perform hata-goster-tarih
                                move 4   to accept-control
                                move 303 to control-id
                             else
                                move rapor-cikis-tarih to takvim-anah
                                perform hata-kontrol-tarih
                                if Hata-Var
                                   perform hata-goster-tarih
                                   move 4   to accept-control
                                   move 306 to control-id
                                else
                                   perform hata-kontrol-depart
                                   if Hata-Var
                                      perform hata-goster-depart
                                   else
                                      perform hata-kontrol-depart-ba
                                      if Hata-Var
                                         perform hata-goster-depart-ba
                                      else
* Helal be akce.... Buraya kadar hatasiz geldin ya. Helal Sana.........
                                         set Hata-Yok to true
                                      end-if
                                   end-if
                                end-if
                             end-if
                          end-if
                       end-if
                    end-if
                 end-if
              end-if
           end-if
        end-if
     end-if
     .
*
 limit-kaydet.
     if musteri
        perform limit-kaydet-musteri
     else
        perform limit-kaydet-odenmez
     end-if
     move 4   to accept-control
     move 101 to control-id
     .
*
 limit-kaydet-musteri.
     move rapor-eklenecek to eklenecek-limit
     if eklenecek-limit > zeroes
        open input depkod
        move rapor-dep-kodu to depkod-anah
        read depkod record no lock
          key is depkod-anah
          invalid
            move all "*" to depkod-rec
        end-read
        close depkod
        initialize exthrk-rec romhrk-rec
        perform fisno-al
        move "M"                  to hrkgir-islem-tipi
        move rapor-folio          to hrkgir-folio
        move rapor-eklenecek      to hrkgir-tl-tutar
        move zeroes               to hrkgir-dv-tutar
        move "A"                  to hrkgir-ba
        move calisma-tarihi       to hrkgir-tarih
        move rapor-dep-kodu       to hrkgir-depkod
        accept hrkgir-zaman       from time
        move rapor-kart-no(bp:au) to hrkgir-kart-no
        move zeroes               to hrkgir-cekno
        move k-kodu-tasi          to hrkgir-staf
        perform tlden-doviz
        if konuk-fol-kodu = "R"
           perform romhrk-yaz
        else
           perform exthrk-yaz
        end-if
     end-if
     if eklenecek-limit = zeroes
        open i-o kart
        perform kart-write
        close kart
     end-if
     .
*
 limit-kaydet-odenmez.
     if onkpara-kart-odenmez-yarat = "E"
        perform odenmez-yaz
     end-if
     open i-o kart
     if Kart-Var
        perform bilgi-aktar-rapor-kart
        compute kart-tutar = kart-tutar + hrkgir-tl-tutar
        rewrite kart-rec invalid continue
        end-rewrite
*        perform log-operation-kart
     else
        perform kart-write
     end-if
     close kart
     .
*
 odenmez-yaz.
     move islem-tipi  to odenmez-ilk
     move rapor-folio to odenmez-kodu
     open i-o odenmez
     read odenmez no lock
       invalid
         initialize odenmez-rec
         move islem-tipi  to odenmez-ilk
         move rapor-folio to odenmez-kodu
         if odenmez-adi = spaces
            move "**Onburo'dan Acilmistir ..." to odenmez-adi
         end-if
         write odenmez-rec,
           invalid
             continue
         end-write
       not invalid
         rewrite odenmez-rec,
           invalid
             continue
         end-rewrite
*         perform log-operation-odenmez
     end-read
     close odenmez
     .
*
 fisno-al.
     open i-o genelfis
     move 1 to genelfis-anahtar
     read genelfis no lock
       invalid
         initialize genelfis-rec
         move 1 to genelfis-anahtar
     end-read
     add 1 to cekgir-oto
     move cekgir-oto to hrkgir-islem
     rewrite genelfis-rec
       invalid
         write genelfis-rec, end-write
     end-rewrite
     close genelfis
     .
*
 tlden-doviz.
     move hrkgir-tarih  to kur-tarih
     move onkpara-banka to kur-banka
     move onkpara-doviz to kur-doviz
     open input kur
     read kur no lock
       invalid
         perform kur-uyari
         move hrkgir-tl-tutar to hrkgir-dv-tutar
       not invalid
         initialize islem-kuru
         move doviz-alis to islem-kuru
         evaluate onkpara-d-e
           when "D"
             evaluate onkpara-a-s
               when "A"
                 move doviz-alis    to islem-kuru
               when "S"
                 move doviz-satis   to islem-kuru
             end-evaluate
           when "E"
             evaluate onkpara-a-s
               when "A"
                 move efektif-alis  to islem-kuru
               when "S"
                 move efektif-satis to islem-kuru
             end-evaluate
         end-evaluate
         initialize hrkgir-dv-tutar
         compute hrkgir-dv-tutar = hrkgir-tl-tutar / islem-kuru
     end-read
     close kur
     .
*
 kur-uyari.
     display
       message box hrkgir-gun "/" hrkgir-ay "/" hrkgir-yil
                   " Tarihli Gunluk Kur Kaydi Bulunamadi..."
       title   = "UYARI"
       type    = MB-OK
       icon    = MB-WARNING-ICON
       default = MB-OK
     .
*
 romhrk-yaz.
     open i-o romhrk
     move hrkgir-rec to romhrk-rec
     write romhrk-rec
       invalid
         perform hata-goster-folio-yazilamadi
       not invalid
         perform onkasa-artir
         perform konuk-artir
         perform Hata-Kontrol-Kart-No
         open i-o kart
         if Kart-Var
            perform bilgi-aktar-rapor-kart
            compute kart-tutar = kart-tutar + hrkgir-tl-tutar
            rewrite kart-rec invalid continue 
            end-rewrite
*            perform log-operation-kart
         else
            perform kart-write
         end-if
         close kart
         perform makbuz-cik
     end-write
     close romhrk
     .
*
 exthrk-yaz.
     open i-o exthrk
     move hrkgir-rec to exthrk-rec
     write exthrk-rec
       invalid
         perform hata-goster-folio-yazilamadi
       not invalid
         perform onkasa-artir
         perform konuk-artir
         perform Hata-Kontrol-Kart-No
         open i-o kart
         if Kart-Var
            perform bilgi-aktar-rapor-kart
            compute kart-tutar = kart-tutar + hrkgir-tl-tutar
            rewrite kart-rec invalid continue
            end-rewrite
*            perform log-operation-kart
         else
            perform kart-write
         end-if
         close kart
         perform makbuz-cik
     end-write
     close exthrk
     .
*
 onkasa-artir.
     open i-o onkasa
     move hrkgir-tarih  to onkasa-tarih
     move hrkgir-depkod to onkasa-dep
     read onkasa no lock
       invalid
         perform onkasa-yeni-isle
       not invalid
         add hrkgir-tl-tutar to onkasa-tutar-tl
         add hrkgir-dv-tutar to onkasa-tutar-dv
         rewrite onkasa-rec
           invalid
             write onkasa-rec, end-write
         end-rewrite
     end-read
     close onkasa
     .
*
 onkasa-yeni-isle.
     initialize onkasa-rec
     move hrkgir-tarih    to onkasa-tarih
     move hrkgir-depkod   to onkasa-dep
     move hrkgir-tl-tutar to onkasa-tutar-tl
     move hrkgir-dv-tutar to onkasa-tutar-dv
     write onkasa-rec
       invalid
         rewrite onkasa-rec,end-rewrite
     end-write
     .
*
 konuk-artir.
     move hrkgir-folio to konuk-folio
     open i-o konuk
     read konuk no lock
       invalid
         continue
       not invalid
         evaluate hrkgir-ba
           when "B"
             add hrkgir-tl-tutar to konuk-top-borc
           when "A"
             add hrkgir-tl-tutar to konuk-top-alac
         end-evaluate
         rewrite konuk-rec, end-rewrite
     end-read
     close konuk
     .
*
 kart-write.
     initialize kart-rec
     perform bilgi-aktar-rapor-kart
     move rapor-indirim        to kart-ind
     move rapor-eklenecek      to kart-tutar
     move zeroes               to kart-limit
     move "E"                  to kart-happy
     move "T"                  to kart-happy-ind
     accept kart-aciklama from time
     move calis-gun            to kart-aciklama(12:)
     move "/"                  to kart-aciklama(14:)
     move calis-ay             to kart-aciklama(15:)
     move "/"                  to kart-aciklama(17:)
     move calis-yil            to kart-aciklama(18:)
     write kart-rec
     invalid
       perform hata-goster-kart-tanitilamadi
     end-write
*     perform log-operation-kart.
     .
*
 bilgi-aktar-rapor-kart.
     move islem-tipi           to kart-tipi
     move rapor-folio          to kart-folio
     move rapor-kart-no(bp:au) to kart-no
     move rapor-giris-tarih    to kart-cin
     move rapor-cikis-tarih    to kart-cot
     if rapor-limit-kontrol-var
        move "E"               to kart-limit-uyari
     else
        move "H"               to kart-limit-uyari
     end-if
     move rapor-buyuk          to kart-buyuk
     move rapor-kucuk          to kart-kucuk
     move rapor-free           to kart-free
     move rapor-indirim        to kart-ind
     move k-kodu-tasi          to kart-staf
     .
*
 makbuz-cik.
     display
       message box "Fis ister misiniz ?"
       title   = "Fis Onayi"
       type    = MB-YES-NO
       icon    = MB-DEFAULT-ICON
       default = MB-YES
       giving  tus

     if tus = MB-YES
        perform fis-yaz
     else
        move 4   to accept-control
        move 101 to control-id
     end-if
     .
*
 fis-yaz.
* Dovizli calisiliyorsa Dovizli Rakam Goster....
     if genel-dv-calis
        move calisma-tarihi to hrkgir-tarih
        perform tlden-doviz
        compute toplam-limit-dv = hrkgir-dv-tutar + onceki-limit-dv
        move onceki-limit-dv to dv-rulo-onceki
        move hrkgir-dv-tutar to dv-rulo-tutar
        move toplam-limit-dv to dv-rulo-balance
     else
        initialize              dv-rulo-onceki
                                dv-rulo-tutar
                                dv-rulo-balance
     end-if
* Dovizli calisiliyorsa Dovizli Rakam Goster....

********************
**  DOKUMER init  **
********************
     open i-o genelfis
     move 1 to genelfis-anahtar
     read genelfis no lock
       invalid
         accept print-no from time
       not invalid
         add 1 to print-no
         rewrite genelfis-rec, end-rewrite
     end-read
     close genelfis
     
     initialize dokumer-rec dokumer-anah
                dokumer-ozellikler-rec
                dokumer-dosya

     set  dokumer-asya-set    to true
     set  dokumer-yazici-text to true
     move "H" to dokumer-genel-baslik-yaz
     move print-no            to dokumer-dosya-adi
     open output dokumer

*/ WINDOW TITLE
     initialize dokumer-rec detay
     move "W"             to det-dokumer-20(1:)
     move "ADVANCE FORM"  to det-filler
     write dokumer-rec from detay, end-write

*/ DOKUMER OZELLIKLER-REC
     initialize dokumer-rec detay
     move "O" to det-dokumer-20(1:)
     move "E" to dokumer-oto-sayfa-basi
     move 56  to dokumer-oto-sayfa-satir
     move "|" to dokumer-detay-kolon-ayirici
     move "L" to dokumer-align-occ
     move dokumer-ozellikler-rec to det-filler
     write dokumer-rec from detay

*/ DOKUMER SATIRLAR
     initialize dokumer-rec detay
     move all "-"  to det-1
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move "A D V A N C E  F O R M" to det-1(10:)
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move all "-"  to det-1
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     accept rapor-zaman from time
     move calis-gun      to rulo-gun
     move calis-ay       to rulo-ay
     move calis-yil      to rulo-yil
     move rapor-saat     to rulo-saat
     move rapor-dakika   to rulo-dakika
     move rapor-saniye   to rulo-saniye
     move rulo-det1      to det-1
     write dokumer-rec from detay, end-write
     
     initialize dokumer-rec detay
     move hrkgir-islem   to rulo-fis
     move rulo-det2      to det-1
     write dokumer-rec from detay, end-write
     
     initialize dokumer-rec detay
     move rapor-oda-no   to rulo-oda
     move rapor-folio    to rulo-folio
     move rulo-det3      to det-1
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move rapor-adi      to rulo-adi
     move rapor-soyadi   to rulo-soyadi
     move rulo-det4      to det-1
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move rapor-limit    to rulo-onceki
     move rulo-det6      to det-1
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move rapor-eklenecek to rulo-tutar
     move rulo-det7       to det-1
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move rapor-toplam   to rulo-balance.
     move rulo-det8      to det-1
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move rulo-det9      to det-1
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     move all "-"  to det-1
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     write dokumer-rec from detay, end-write

     initialize dokumer-rec detay
     write dokumer-rec from detay, end-write

     close dokumer
     call dokumer-prog
       on exception
          perform callerr-proc
       not on exception
          cancel dokumer-prog
     end-call
     delete file dokumer
     .
*
 kodlar02-alkol-kontrol.
        if kodlar02-alkol = 0 or
           kodlar02-alkol = 1 or 
           kodlar02-alkol = 2 or 
           kodlar02-alkol = 3 
           continue
        else 
           move 3 to kodlar02-alkol
        end-if.
*
 Form1-Aft-Routine.
     close odalar2.
     .
 
