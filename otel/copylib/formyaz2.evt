* formyaz.evt
* formyaz.evt is generated from C:\Asya\acugt.ytl\otel\formyaz.Psf
* This is a generated file. DO NOT modify this file directly.


 Form1-Event-Proc.
* 
     EVALUATE Event-Type
     WHEN Ntf-Resized
        PERFORM Form1-Ev-Ntf-Resized
     END-EVALUATE
     .

 Form1-Exception-Proc.
* 
     IF Event-Occurred
        EVALUATE Event-Type
        WHEN Ntf-Resized
           PERFORM Form1-Ev-Ntf-Resized
        END-EVALUATE
     ELSE
        PERFORM Ex-Oth-Form1
     END-IF
     .

 cb-dzn-Exception-Proc.
* 
     IF Event-Occurred
        EVALUATE Event-Type
        WHEN Ntf-Selchange
           PERFORM Aft-Proc-Dzn
        END-EVALUATE
     ELSE
        PERFORM Ex-Oth-Form1
     END-IF
     .

 Form3-Event-Proc.
     .

 Form3-Exception-Proc.
     PERFORM Form3-Ex-Other
     .
***   start event editor code   ***
*
 Bef-Create-Form1.
     perform adresleri-tasi
     move dosya-no to formyaz-dosya-no
                      dzn-dosya-no
                      sayf-dosya-no
     move ara-dosya-no         to ara-no
     open i-o formyaz
     perform isle-modul
     perform isle-ozellikler
     perform bul-max-detay
     close formyaz

     accept terminal-abilities from terminal-info
     set dzn-size to size of text-oku-rec
       open input genel
     move 1 to genel-anahtar
     read genel no lock
       invalid
         continue
     end-read
     close genel
     .
*
 Form1-Aft-Initdata.
     perform win-max-boyut
     perform init-dzn-sec
     perform oku-dzn-sec
     perform aktar-text-dzn
     perform aktar-sayfalar
     move 1 to islem-sayfa
     perform aktar-sayfa-grid
     display acc-sayfa
     perform yaz-fatura
*/-----------------------------\*  
*/---------DIL AYARLA----------\*    
*/-----------------------------\*    
     perform dil-ayarla-proc.
*/-----------------------------\*         
     .
*
 Form1-Ev-Ntf-Resized.
*     inquire form1-handle,
*       size  in win-now-x
*       lines in win-now-y
*      compute win-disp-x = win-now-x - .1
*     compute win-disp-y = win-now-y - 6.5
*     display form1
     .
*
 Ex-Oth-Form1.
     evaluate key-status
       when 2 perform yaz-fatura
       when 3 perform konumlan-ilk
       when 4 perform konumlan-onceki
       when 5 perform konumlan-sonraki
       when 6 perform konumlan-son
     end-evaluate
     .
*
 Aft-Proc-dzn.
     perform oku-dzn-sec
     perform aktar-text-dzn
     perform aktar-sayfalar
     move 1 to islem-sayfa
     perform aktar-sayfa-grid
     .
*
 win-max-boyut.
     modify Form1-Handle,
       action = action-maximize
     inquire Form1-Handle,
       SIZE  in win-max-x
       LINES in win-max-y
*     modify gd-dzn,
*       size = win-max-x pixels
*     modify Form1-Handle,
*       action = action-restore
     modify Form1-Handle,
       size   = win-max-x
       lines  = win-max-y
     .
*
 init-dzn-sec.
     modify cb-dzn,
       reset-list  = 1
       mass-update = 1
     perform oku-modul-adres
     modify cb-dzn,
       mass-update = 0
     perform oku-dzn-sec
     display cb-dzn
     .
*
 init-grid.
     modify gd-dzn,
       reset-grid  = 1
       mass-update = 1
     .
*
 init-dokumer.
     initialize dokumer-rec
                dokumer-anah
                dokumer-ozellikler-rec
                dokumer-dosya
     set  dokumer-asya-set to true
     move dosya-no         to dokumer-dosya-adi.
     move "H"              to dokumer-genel-baslik-yaz.
     open output dokumer
     .
*
 start-formyaz-tip.
     initialize fs-formyaz
     start formyaz key >= formyaz-tip-sira
       invalid
         move "10" to fs-formyaz
     end-start
     .
*
 start-dzn-tip.
     initialize fs-dzn
     start dzn key >= dzn-tip-sira
       invalid
         move "10" to fs-dzn
     end-start
     .
 oku-modul-adres.
     call "C$LIST-DIRECTORY"  
       using listdir-open, modul-adres, "*.dzn"
     end-call
     move return-code to modul-handle 
     if modul-handle not = 0 
        perform oku-dzn-adi
        call "C$LIST-DIRECTORY"
          using listdir-close, modul-handle
        end-call
     end-if
     .
*
 oku-dzn-adi.
     initialize modul-dosya
     perform with test after
             until modul-dosya = spaces 
       call "C$LIST-DIRECTORY"  
         using listdir-next, modul-handle, modul-dosya
       end-call
       if modul-dosya not = spaces
          perform yaz-dzn-sec
       end-if
     end-perform
     .
*
 oku-dzn-sec.
     inquire cb-dzn,
       value in modul-dosya
     initialize text-oku-dosya-adres
     evaluate true
       when rap-fat-ozet
            move "o" to modul-tipi
       when rap-fat-detay
            move "d" to modul-tipi
       when rap-fat-ozet
            move "e" to modul-tipi
     end-evaluate
     if modul-dosya = spaces
        string
          modul-adi   delimited by space
          modul-tipi  delimited by space
          ".dzn"      delimited by size
          into text-oku-dosya-adres
        end-string
        move text-oku-dosya-adres to modul-dosya
     else
        string
          modul-adi   delimited by space
          "/"         delimited by size
          modul-dosya delimited by space
          into text-oku-dosya-adres
        end-string
     end-if
     .
*
 oku-formyaz.
     read formyaz no lock
       invalid
         perform ara-form-data
     end-read
     .
*
 oku-formyaz-next.
     read formyaz next no lock
       end
         move "10" to fs-formyaz
     end-read
     .
*
 oku-dzn-next.
     read dzn next no lock
       end
         move "10" to fs-dzn
     end-read
     .
*
 oku-sayf.
     read sayf no lock
       invalid
         continue
     end-read
     .
*
 oku-text-oku.
     initialize text-oku-rec
     read text-oku next no lock
       end
         move "10" to fs-text-oku
     end-read
     .
*
 oku-konuk.
     read konuk no lock
       invalid
         continue
     end-read
     .
*
 yaz-formyaz.
     write formyaz-rec
       invalid
         rewrite formyaz-rec, end-rewrite
     end-write
     .
*
 yaz-dzn.
     write dzn-rec
       invalid
         rewrite dzn-rec, end-rewrite
     end-write
     .
*
 yaz-sayf.
     write sayf-rec
       invalid
         rewrite sayf-rec, end-rewrite
     end-write
     .
*
 yaz-dzn-sec.
     modify cb-dzn,
       item-to-add = modul-dosya
     .
*
 yaz-fatura.
    
     if mami-hata = 1 then
        display message  box " Lutfen Yeniden Deneyiniz"
        set exit-pushed to true
        exit paragraph
     end-if
     move 0 to doksay
     open i-o fatura ara
     perform acu-form3-routine.
     if fatura-seri-donus <> "E"
        close fatura ara
        exit paragraph
     end-if
     perform fatura-dosyasi-kontrol.
     perform fatura-baslik-yaz.

     perform init-dokumer
     perform ata-dokumer-ozellik
     open input sayf
     initialize sayf-rec
     set form-devam to true
     move 1 to islem-sayfa
     perform with test before
             until form-bitti
       start sayf key >= sayf-anah
         invalid
           set form-bitti to true
         not invalid
           read sayf next no lock
             end
               set form-bitti to true
             not end
               if sayf-sayfa = islem-sayfa
                 perform aktar-sayfa-dokumer
                 
*                 initialize dokumer-rec
*                 set dokumer-20-sayfa-basi to true
*                 write dokumer-rec  end-write
                
               else
                 set form-bitti to true
               end-if
           end-read
       end-start
       add 1 to islem-sayfa
       initialize sayf-rec
       move islem-sayfa to sayf-sayfa
     end-perform
     perform write-to-romhrk-exthrk
     close sayf fatura ara
     perform cagir-dokumer

     set exit-pushed to true
     .
*
 bul-max-detay.
     move all high-value to formyaz-rec
     set formyaz-detay to true
     start formyaz key <= formyaz-tip-sira
       invalid
         initialize formyaz-rec
       not invalid
         read formyaz previous no lock
           end
             initialize formyaz-rec
         end-read
     end-start
     move formyaz-sira to max-detay
     .
*
 bul-fark-satir.
     move all high-value to sayf-rec
     set sayf-detay to true
     move islem-sayfa to sayf-sayfa
     start sayf key <= sayf-tip-sira
       invalid
         move zero to a15v2
       not invalid
         read sayf previous no lock
           end
             move zero to a15v2
           not end
             if not sayf-detay or
                sayf-sayfa not = islem-sayfa
                move zero to a15v2
             else
                move sayf-satir to a15v2
             end-if
         end-read
     end-start

     initialize dzn-rec
     set dzn-sonuc to true
     start dzn key >= dzn-tip-sira
       invalid
         move zero to b15v2
       not invalid
         read dzn next no lock
           end
             move zero to b15v2
           not end
             if not dzn-sonuc
                move zero to b15v2
             else
                move dzn-satir to b15v2
             end-if
         end-read
     end-start
     if a15v2 = zeroes or
        b15v2 = zeroes
        initialize fark-satir
     else
        compute fark-satir rounded
              = b15v2 - a15v2
        end-compute
     end-if
     .
*
 bul-oran.
     initialize formyaz-rec
     set formyaz-ara-top to true
     move "NET" to formyaz-kodu
     move islem-sayfa to formyaz-sira
     start formyaz key <= formyaz-tip-sira
       invalid
         initialize b15v2
       not invalid
         read formyaz previous no lock
           end
             initialize b15v2
           not end
             if formyaz-sira not = islem-sayfa or
                not formyaz-ara-top
                initialize b15v2
             else
               perform Cevir-Z15v2
               move a15v2 to b15v2
             end-if
         end-read
     end-start

     move formyaz-kodu to sakla-kodu
     set formyaz-gen-top to true
     move zero to formyaz-sira
     start formyaz key <= formyaz-tip-sira
       invalid
         initialize a15v2
       not invalid
         read formyaz previous no lock
           end
             initialize a15v2
           not end
             if formyaz-sira not = zero or
                formyaz-kodu not = sakla-kodu or
                not formyaz-gen-top
                initialize a15v2
             else
               perform Cevir-Z15v2
             end-if
         end-read
     end-start

     initialize oran
     compute oran rounded
           = b15v2 / a15v2
     end-compute
     .
*
 ata-dokumer-ozellik.
*/ DOKUMER OZELLIKLER-REC
     initialize dokumer-rec
     set dokumer-20-ozellik          to true
     set dokumer-oto-sayfa-basi-yap  to TRUE
     if onkpara-cift = 1 then 
            set dokumer-manuel-sayfa-basi-yap to true
     end-if
     move sayfa-boyu  to dokumer-oto-sayfa-satir
     move "|"         to dokumer-detay-kolon-ayirici
     move "L"         to dokumer-align-occ
     write dokumer-rec, end-write
     .
*
 ata-dzn-sayf.
     initialize sayf-rec
                formyaz-rec
     if dzn-detay
        move islem-detay to formyaz-sira
     end-if
     move dzn-tipi       to sayf-tipi
                            formyaz-tipi
     move dzn-kodu       to formyaz-kodu
     if dzn-sonuc and
        fark-satir not = zeroes
        compute sayf-satir
              = ( dzn-satir - fark-satir ) + 1
        end-compute
     else
        move dzn-satir   to sayf-satir
     end-if
     move islem-sayfa    to sayf-sayfa

     if dzn-ara-top
        move islem-sayfa to formyaz-sira
     end-if

     perform oku-formyaz

     if dzn-gen-top and
        form-devam and
        rap-nakli-yekun
        initialize formyaz-deger
     end-if

     perform oku-sayf
     if islem-sabit and
        dzn-detay   and
        sayf-data = spaces
        initialize formyaz-deger
     end-if
     evaluate dzn-kodu
       when "MAT"
       when "INO"
       when "IND"
       when "NET"
       when "NE8"
       when "N18"
       when "KDO"
       when "KDV"
       when "KD8"
       when "TUT"
       when "GEN"
         perform cevir-z15v2
         if dzn-gen-top and
            rap-nakil-yok
            compute b15v2 rounded
                  = a15v2 * oran
            end-compute
            move b15v2 to z15v2
            move z15v2 to formyaz-deger
            perform cevir-z15v2
         end-if
       when "PAX"
       when "CHI"
         perform cevir-z15
       when "FTT"
       when "CIN"
       when "COT"
         perform cevir-tarih
     end-evaluate


    
     move formyaz-deger(1:dzn-au) to sayf-data(dzn-bp:dzn-au)
     if dzn-detay
        evaluate dzn-kodu
          when "MAT"
          when "INO"
          when "IND"
          when "NET"
          when "KDO"
          when "KDV"
          when "KD8"
          when "NE8"
          when "N18"
          when "TUT"
          when "GEN"
            perform ara-toplam-al
        end-evaluate
     end-if
     .
*
 cevir-z15v2.
     compute a15v2
           = function numval-c(formyaz-deger)
     end-compute
     move a15v2               to z15v2
     initialize formyaz-deger
     if dzn-au > 22
        move 22               to dzn-au
     end-if
     move z15v2(23 - dzn-au:) to formyaz-deger
     if a15v2 = zeroes
        initialize formyaz-deger
     end-if
     .
*
 cevir-z15.
     compute a15v2
           = function numval-c(formyaz-deger)
     end-compute
     move a15v2               to z15
     initialize formyaz-deger
     if dzn-au > 19
        move 19               to dzn-au
     end-if
     move z15(20 - dzn-au:) to formyaz-deger
     if a15v2 = zeroes
        initialize formyaz-deger
     end-if
     .
*
 cevir-tarih.
     move formyaz-deger(1:4)  to z-yil
     move formyaz-deger(5:2)  to z-ay
     move formyaz-deger(7:2)  to z-gun
     initialize formyaz-deger
     if (z-gun = spaces or z-gun = zeroes) and
        (z-ay  = spaces or z-ay  = zeroes) and
        (z-yil = spaces or z-yil = zeroes)
        initialize formyaz-deger
     else
        move z-tarih             to formyaz-deger
     end-if
     .
*
 ara-toplam-al.
     move a15v2 to b15v2
     initialize formyaz-deger
     set formyaz-ara-top to true
     move islem-sayfa    to formyaz-sira
     perform oku-formyaz
     perform cevir-z15v2
     add a15v2           to b15v2 giving t15v2
     initialize formyaz-deger
     move t15v2          to z15v2
     move z15v2          to formyaz-deger
     if t15v2 = zeroes
        initialize formyaz-deger
     end-if
     move islem-sayfa    to formyaz-sira
     perform yaz-formyaz
     .
*
 aktar-text-dzn.
     open output dzn for bulk-addition
     open input text-oku
     open i-o formyaz
     initialize fs-text-oku
     initialize dzn-rec
     perform with test after
             until fs-text-oku = "10"
       perform oku-text-oku
       if fs-text-oku not = "10"
          perform aktar-satir-alan
       end-if
     end-perform
     close formyaz
     close text-oku
     close dzn
     .
*
 aktar-satir-alan.
     move text-oku-rec(1:1) to dzn-tipi
     move text-oku-rec(2:1) to dzn-tip
     evaluate true
       when dzn-tanim perform islem-tanim
       when dzn-yeni  perform islem-yeni
       when dzn-devam perform islem-devam
     end-evaluate
     .
*
 aktar-sayfalar.
     open output sayf
     close sayf
     open i-o sayf
     open i-o dzn
     open i-o formyaz
     initialize islem-sayfa
                islem-detay
     set form-devam     to true
     set islem-degisken to true
     perform with test before
             until form-bitti

       initialize dzn-rec
       set dzn-baslik  to true
       perform isle-satirlar

       initialize dzn-rec
       set dzn-detay   to true
       perform isle-satirlar

       initialize dzn-rec
       set dzn-sonuc   to true
       perform isle-satirlar

       initialize dzn-rec
       set dzn-ara-top to true
       perform isle-satirlar

       perform bul-oran
       
       initialize dzn-rec
       set dzn-gen-top to true
          
       perform isle-satirlar
       
     end-perform
     close formyaz
     close dzn
     close sayf
     .
*
 aktar-sayfa-grid.
     open i-o sayf
     perform init-grid
     perform varying islem-satir
                from 1
                  by 1
               until islem-satir > sayfa-boyu
       initialize sayf-rec
       move islem-sayfa to sayf-sayfa
       move islem-satir to sayf-satir
       read sayf no lock
         invalid
           move all high-value to sayf-rec
       end-read
       move sayf-data to gd-detay
       modify gd-dzn
         record-to-add = gd-rec
     end-perform
     modify gd-dzn
       mass-update = 0
     close sayf
     move 4    to accept-control
     move 2011 to control-id
     .
*
 aktar-sayfa-dokumer.
     perform varying islem-satir
                from 1
                  by 1
               until islem-satir > sayfa-boyu
       initialize sayf-rec
                  dokumer-rec
       move islem-sayfa to sayf-sayfa
       move islem-satir to sayf-satir
       read sayf no lock
         invalid
           initialize sayf-rec
       end-read

       if sayf-detay
          move acc-fatura-no  to fatura-no
          add 1               to fatura-sira
          perform ara-to-fatura
          move sayf-data      to fatura-aciklama
     
          write fatura-rec invalid
                rewrite fatura-rec end-rewrite
          end-write
       end-if

       move sayf-data to dokumer-400
        if onkpara-cift = 1 then 
    
       add 1 to doksay
       move dokumer-400 to bufdok(doksay)
        end-if

       write dokumer-rec, end-write
     end-perform
     .
*
 islem-tanim.
     initialize formyaz-rec
     move text-oku-rec(3:3) to formyaz-kodu
     move text-oku-rec(7:)  to formyaz-deger
     move "B"               to formyaz-tipi 
     perform yaz-formyaz
     move "D"               to formyaz-tipi 
     perform yaz-formyaz
     move "A"               to formyaz-tipi 
     perform yaz-formyaz
     move "G"               to formyaz-tipi 
     perform yaz-formyaz
     move "S"               to formyaz-tipi 
     perform yaz-formyaz
     move "O"               to formyaz-tipi 
     perform yaz-formyaz
     .
*
 islem-yeni.
     add 1 to dzn-satir
     perform islem-devam
     .
*
 islem-devam.
     perform varying bp
                from 2
                  by 9
               until bp > ( dzn-size - 9 )
       move text-oku-rec( bp + 1:3) to dzn-kodu
       move text-oku-rec( bp + 4:3) to dzn-bp
       move text-oku-rec( bp + 7:2) to dzn-au
       evaluate dzn-kodu
         when spaces
              continue
         when "FUZ"
              perform islem-form-uzunluk
         when "NL "
              perform islem-bos-ekle
         when "LOP"
              perform islem-lop
         
         when other
*            if text-oku-rec(2:3) = "EOF" then stop " " end-if
              perform yaz-dzn
       end-evaluate
     end-perform
     .
*
 islem-form-uzunluk.
     compute sayfa-boyu
           = ( dzn-bp * 100 ) + dzn-au
     end-compute
     .
*
 islem-bos-ekle.
     compute dzn-satir
           = dzn-satir 
             + ((( dzn-bp * 100 ) + dzn-au ) - 1 )
     end-compute
     .
*
 islem-lop.
     compute lop-sayi
           = ( dzn-bp * 100 ) + dzn-au
     end-compute
     perform oku-text-oku
     if fs-text-oku not = "10"
        perform lop-sayi times
          perform aktar-satir-alan
        end-perform
     end-if
     .
*
 isle-modul.
     initialize formyaz-rec
     set formyaz-modul to true
     move "PRG" to formyaz-kodu
     perform oku-formyaz
     move formyaz-deger to modul-adi
     .
*
 isle-ozellikler.
     initialize formyaz-rec
     set formyaz-ozellik to true
     perform start-formyaz-tip
     perform with test before
             until fs-formyaz = "10"
       perform oku-formyaz-next
       if not formyaz-ozellik
          move "10" to fs-formyaz
       end-if
       if fs-formyaz not = "10"          
          evaluate formyaz-kodu
            when "FDT"
                 compute rap-fat-tipi
                       = function numval(formyaz-deger)
                 end-compute
            when "FNY"
                 compute rap-nakil-status
                       = function numval(formyaz-deger)
                 end-compute
          end-evaluate
       end-if
     end-perform
     initialize fs-formyaz
     .
*
 isle-satirlar.
     if dzn-sonuc
        perform bul-fark-satir
     end-if
     initialize islem-satir
     move dzn-tipi to sakla-tipi
     if dzn-baslik
        add 1 to islem-sayfa
     end-if
     
     perform start-dzn-tip
     if dzn-detay and
        fs-dzn = "10"
        set form-bitti to true
     end-if
     perform with test before
             until fs-dzn = "10"
       perform oku-dzn-next
       if dzn-tipi not = sakla-tipi
          move "10" to fs-dzn
       end-if
       
       if fs-dzn not = "10"
          if dzn-bp not = zeroes and
             dzn-au not = zeroes
             if dzn-detay
                if islem-satir not = dzn-satir
                   add 1 to islem-detay
                   move dzn-satir to islem-satir
                end-if
             end-if
             perform ata-dzn-sayf
             if dzn-detay and
                sayf-data = spaces
                continue
             else
               perform yaz-sayf
             end-if
          end-if
       end-if
     end-perform
     initialize fs-dzn
     move sakla-tipi to dzn-tipi
     if islem-detay >= max-detay and
        dzn-detay
        set form-bitti to true
     end-if
     .
*
 ara-form-data.
     set islem-degisken to true
     evaluate modul-adi
       when "folfat"
       when other
         evaluate formyaz-kodu
           when "MAD"
           when "MAS"
           when "ODA"
           when "CIN"
           when "COT"
             perform ac-konuk
         end-evaluate
     end-evaluate

     evaluate modul-adi
       when "folfat"
       when other
         evaluate formyaz-kodu
           when "FTT"
             accept f-tarih from date yyyymmdd
             move f-tarih        to formyaz-deger
           when "MAD"
             move konuk-adi      to formyaz-deger
           when "MAS"
             move konuk-soyadi   to formyaz-deger
           when "ODA"
             move konuk-odano    to formyaz-deger
           when "CIN"
             move konuk-gel-tar  to formyaz-deger
           when "COT"
             move konuk-git-tar  to formyaz-deger
           when "VOU"
             move konuk-voucher  to formyaz-deger
           when "PAN"
             move konuk-pan-tipi to formyaz-deger
           when "PAX"
             move konuk-buyuk    to formyaz-deger
           when "CHI"
             move konuk-kucuk    to formyaz-deger
*           when "FTA" 
*             move fatura-ack       to formyaz-deger
           when "PR "
             initialize formyaz-deger
             set formyaz-gen-top to true
             move "GEN" to formyaz-kodu
             perform oku-formyaz
             compute rakam
                   = function numval-c(formyaz-deger)
             end-compute
             if rap-nakil-yok
                compute rakam rounded
                      = rakam * oran
                end-compute
             end-if
             if rakam not = gtop-gen
               move 1 to mami-hata
             end-if  
             call "/asya/ytl/obj/otel/yaziile.asy"
               using rakam yaziile
               on exception
                 perform callerr-proc
               not on exception
                 cancel "/asya/ytl/obj/otel/yaziile.asy" 
             end-call
      
             move yaziile to formyaz-deger
           when other
             if formyaz-detay and
                formyaz-sira not = zeroes
                move zero        to formyaz-sira
                perform oku-formyaz
                move islem-detay to formyaz-sira
                set islem-sabit  to true
             end-if
         end-evaluate
     end-evaluate
     .
*
 ac-konuk.
     move formyaz-kodu to sakla-kodu
     move "FOL" to formyaz-kodu
     perform oku-formyaz
     move sakla-kodu   to formyaz-kodu
     open i-o konuk
     initialize konuk-rec
     compute konuk-folio
           = function numval(formyaz-deger)
     end-compute
     perform oku-konuk
     close konuk
     .
*
 cagir-dokumer.
     
      if onkpara-cift = 1 
          initialize dokumer-rec
            write dokumer-rec, end-write
              write dokumer-rec, end-write
                write dokumer-rec, end-write

            perform varying zi from 1 by 1 until  zi > doksay
                
                 move  bufdok(zi)   to   dokumer-400 
                  write dokumer-rec, end-write
             end-perform 
       end-if
     close dokumer
     
       open i-o genel
    move 1 to genel-anahtar
    read genel no lock invalid continue
         not invalid continue
    end-read
      move acc-fatura-no to genel-fol-fat-no 
      rewrite genel-rec
    close genel.



     set dokumer-yazici-text to true
     call dokumer-prog
       on exception
         perform callerr-proc
       not on exception
         cancel dokumer-prog
     end-call
  
     delete file dokumer
     .
*
 hata-yaz-formyaz.
     display message box
       formyaz-kodu " daha once kullanilmis", new-line
       "Lutfen DZN dosyasinda farkli bir kod kullanin"
       title "!!! HATA !!!"
       icon  mb-error-icon
     .
*
 konumlan-ilk.
     move 1 to islem-sayfa
     perform aktar-sayfa-grid
     display acc-sayfa
     .
*
 konumlan-onceki.
     subtract 1 from islem-sayfa
     if islem-sayfa = zero
        move 1 to islem-sayfa
     end-if
     perform aktar-sayfa-grid
     display acc-sayfa
     .
*
 konumlan-sonraki.
     open i-o sayf
     initialize sayf-rec
     move islem-sayfa    to sayf-sayfa
     move all high-value to sayf-satir
     start sayf key > sayf-anah
       invalid
         continue
       not invalid
         read sayf next no lock
           end
             continue
           not end
             move sayf-sayfa to islem-sayfa
         end-read
     end-start
     close sayf
     perform aktar-sayfa-grid
     display acc-sayfa
     .
*
 konumlan-son.
     open i-o sayf
     initialize sayf-rec
     move all high-value to sayf-sayfa
                            sayf-satir
     start sayf key < sayf-anah
       invalid
         continue
       not invalid
         read sayf previous no lock
           end
             continue
           not end
             move sayf-sayfa to islem-sayfa
         end-read
     end-start
     close sayf
     perform aktar-sayfa-grid
     display acc-sayfa
     .
*
 konumlan-sayfa.
     perform aktar-sayfa-grid
     display acc-sayfa
     .
*
 Form1-Aft-Routine.
    delete file dzn
    delete file sayf
    .
*



 Form3-Bef-Create.
     initialize fatura-seri-donus acc-fatura-no.
     
     .
*
 Form3-Ex-Other.
     if key-status <> 2
        exit paragraph.
     if acc-fatura-no = zeroes
        move 4 to accept-control
        move 5 to control-id
        exit paragraph.

     initialize fatura-rec
     move acc-fatura-no        to fatura-no
     move 0                    to fatura-sira
     read fatura no lock invalid
          display message box 
                  acc-fatura-no," seri numarali fatura tanimlamasi yapilmamis"
                  icon mb_error_icon
                  title "Hata"
          move 4 to accept-control
          move 5 to control-id
          exit paragraph
     end-read
     evaluate fatura-durumu
     when "I"
        display message box 
                acc-fatura-no," numarali fatura iptal edilmis !"
                icon mb_error_icon
                title "Hata"
        move 4 to accept-control
        move 5 to control-id
        exit paragraph

     when "D"
        display message box 
                acc-fatura-no," seri numarali fatura daha once kesilmis !",
                new-line,
                new-line,
                "Kesilmis fatura Bilgileri"
                new-line,
                "Fatura Tarihi :",x"09",fatura-gun,"/"fatura-ay,"/",fatura-yil
                new-line,
                "Folio :",x"0909",fatura-folio,
                new-line,
                fatura-baslik-1, new-line,
                fatura-baslik-2, new-line,
                "Adres 1 :", x"09",fatura-adres-1,  new-line,
                "Adres 2 :", x"09",fatura-adres-2,  new-line,
                "Il :",      x"09",fatura-il,       new-line,
                "Ilce :",    x"09",fatura-ilce,     new-line,
                "Ulke :",    x"09",fatura-ulke,     new-line,
                "Vergi D.:", x"09",fatura-ver-da,   new-line,
                "Vergi N. :",x"09",fatura-ver-no    new-line new-line
                "Fatura tekrar kesilsin mi ?"
                type mb_yes_no
                default 2
                returning return-code
        if return-code <> 1
           move 4 to accept-control
           move 5 to control-id
           exit paragraph
        end-if
                
     end-evaluate.
     move "E"        to fatura-seri-donus.
     set exit-pushed to true.
*

*
*( daha once kesildiyse eski fatura bilgilerini siliverezü
 fatura-dosyasi-kontrol.
    initialize fatura-rec
    move acc-fatura-no              to fatura-no
    move 1                          to fatura-sira
    start fatura key not < fatura-anah invalid
          continue
    not invalid 
    initialize fs-fatura
    perform with test after until fs-fatura = "10"
    read fatura next no lock end move "10" to fs-fatura
    not at end
         if fatura-no <> acc-fatura-no
            exit perform
         end-if
         delete fatura end-delete
    end-read
    end-perform
    end-start.
*
 fatura-baslik-yaz.
    initialize ara-rec
    read ara no lock invalid
         continue
    end-read
*/ Foliolari sirasi 999 da sakliyoruz.... 15 folio yeter
    move acc-fatura-no                to fatura-no
    move 999                          to fatura-sira
    move 1                            to ii
    perform varying i
            from 1
            by 1
            until i > 15
            if ara-folio-no(i) <> zeroes
               move ara-folio-no(i) to fatura-aciklama(ii:)
               add 8                to ii
            end-if
    end-perform
    write fatura-rec invalid
          rewrite fatura-rec end-rewrite
    end-write
*/
    move acc-fatura-no                to fatura-no
    move 0                            to fatura-sira
    move ara-fat-tar to gercek-fat-tar

    read fatura no lock invalid
         continue
    end-read
    perform ara-to-fatura
    write fatura-rec invalid 
          rewrite fatura-rec end-rewrite
    end-write.


*
 ara-to-fatura.
    move ara-fat-tar                  to fatura-tarihi
    move "F"                          to fatura-tipi
    move "D"                          to fatura-durumu
    move ara-folio                    to fatura-folio
    move ara-rap-adi                  to fatura-baslik-1 
    move ara-rap-soyadi               to fatura-baslik-2 
    move ara-rap-il                   to fatura-il
    move ara-rap-ilce                 to fatura-ilce
    move ara-rap-adr1                 to fatura-adres-1  
    move ara-rap-adr2                 to fatura-adres-2
    move ara-rap-ulke                 to fatura-ulke     
    move ara-rap-vd                   to fatura-ver-da   
    move ara-rap-vno                  to fatura-ver-no   
    move ara-rap-ack                  to fatura-aciklama 
    move k-kodu-tasi                  to fatura-staf
    move ara-fatura-toplam            to fatura-toplam  
    move ara-fatura-indirim           to fatura-indirim 
    move ara-fatura-matrah            to fatura-matrah  
    move ara-fatura-kdv               to fatura-kdv      
    move ara-fatura-g-toplam          to fatura-g-toplam.
    move ara-fatura-matrah-8            to fatura-matrah-8  
    move ara-fatura-kdv-8               to fatura-kdv-8    
    move ara-fatura-matrah-18            to fatura-matrah-18  
    move ara-fatura-kdv-18               to fatura-kdv-18 .  
*    move ara-folio                    to fatura-folio.

*
 karsilik-bul.
   
     
 

     .




*
 write-to-romhrk-exthrk.
    if debug = 1 then stop " " end-if
    open i-o romhrk exthrk fatdetay
    initialize ara-rec
    initialize son-pencere
    start ara key not < ara-anah invalid
          continue
    not invalid
    initialize fs-ara
    perform with test after until fs-ara = "10"
    read ara next no lock end move "10" to fs-ara
    not at end
         if not ara-islem-secildi 
            exit perform cycle
         end-if

         initialize exthrk-rec romhrk-rec fatdetay-rec

         evaluate ara-romhrk-exthrk
         when "R"
              move ara-folio         to romhrk-folio
              move ara-islem         to romhrk-islem
              read romhrk no lock invalid
                   continue
              not invalid
                   move acc-fatura-no to romhrk-fatura-no
                   rewrite romhrk-rec end-rewrite
              end-read
              move romhrk-TARIH    to  fatdetay-isl-tar
              move romhrk-depkod   to  fatdetay-depkod
                  
                if son-folio not = romhrk-folio or romhrk-ref not = son-pencere
                    move romhrk-folio to son-folio
                    move romhrk-ref to son-pencere 
                     initialize romhrk-rec
                     move son-folio to romhrk-folio

                     start romhrk key > romhrk-anah
                         invalid continue
                         not invalid
                         perform until fs-romhrk = "10"
                          read romhrk next no lock 
                             end move "10" to fs-romhrk
                             not end
                                   if romhrk-folio not = son-folio 
                                     exit perform 
                                   end-if
                                   if romhrk-ref not = son-pencere 
                                      exit perform cycle
                                   end-if
                                   if romhrk-fatura-no = 0 or  romhrk-fatura-no not numeric
                                   move acc-fatura-no to romhrk-fatura-no
                                   rewrite romhrk-rec invalid stop " " 
                                    end-rewrite
                                  end-if
                           end-read
                          end-perform
                     end-start
                 end-if
         when "E"
              move ara-folio         to exthrk-folio
              move ara-islem         to exthrk-islem
              read exthrk no lock invalid
                   continue
              not invalid
                   move acc-fatura-no to exthrk-fatura-no
                   rewrite exthrk-rec end-rewrite
              end-read
              move exthrk-TARIH    to  fatdetay-isl-tar
              move exthrk-depkod   to  fatdetay-depkod
              if son-folio not = exthrk-folio or exthrk-ref not = son-pencere
                    move exthrk-folio to son-folio
                    move exthrk-ref to son-pencere 
                     initialize exthrk-rec
                     move son-folio to exthrk-folio

                     start exthrk key > exthrk-anah
                         invalid continue
                         not invalid
                         perform until fs-exthrk = "10"
                          read exthrk next no lock 
                             end move "10" to fs-exthrk
                             not end
                                   if exthrk-folio not = son-folio 
                                     exit perform 
                                   end-if
                                   if exthrk-ref not = son-pencere 
                                      exit perform cycle
                                   end-if
                                   
                                  
                                  if exthrk-fatura-no = 0 or   exthrk-fatura-no not  numeric
                                   move acc-fatura-no to exthrk-fatura-no
                                   rewrite exthrk-rec invalid stop " " 
                                    end-rewrite
                                  end-if
                           end-read
                          end-perform
                     end-start
                 end-if
           when other 
             display message box "Folio tipi belirsiz"


         end-evaluate
       
         perform karsilik-bul
         accept fatdetay-kes-tarih from century-date
         accept fatdetay-kes-zaman from time   
          
         move gercek-fat-tar    to  fatdetay-fat-tar                    
         move acc-fatura-no     to  fatdetay-fat-no                     
         move ara-folio         to  fatdetay-folio                      
         move ara-islem         to  fatdetay-islem                      
         move ara-romhrk-exthrk to  fatdetay-romhrk-exthrk              
         move ara-kdv-oran      to  fatdetay-kdv-orani                  
         move ara-matrah        to  fatdetay-fatura-matrah              
         move ara-kdv           to  fatdetay-fatura-kdv                 
         move ara-toplam        to  fatdetay-fatura-toplam  
            
         write fatdetay-rec invalid rewrite fatdetay-rec end-write
         
      end-read
    end-perform
    end-start
    close romhrk exthrk fatdetay.
*
 Form3-Aft-Initdata.
    open input genel
    move 1 to genel-anahtar
    read genel no lock invalid continue
         not invalid continue
    end-read
      compute acc-fatura-no  =  genel-fol-fat-no +  1
      display Form3-Ef-1
      close genel.
*/-----------------------------\*  
*/---------DIL AYARLA----------\*    
*/-----------------------------\*    
     perform dil-ayarla-proc.
*/-----------------------------\*         


 
